(excitement_level kExcitementOkay)
(lighter off)
(enter
   {{$this dir} add_sinks $this
      intro_start_msg
      crowd_update
      excitement
      crowd_hide
      crowd_lighters_slow
      crowd_lighters_fast
      crowd_lighters_off
      crowd_half_tempo
      crowd_double_tempo
      crowd_normal_tempo}
   {set [excitement_level] kExcitementOkay}
   {set [lighter] off})
(intro_start_msg
   ($fast $encore)
   {set $crowd.play_flags {'|' kPlayNoBlend kPlayGraphLoop kPlayRealTime}}
   {if_else
      $encore
      {do
         {set [excitement_level] kExcitementGreat}
         {$this crowd_update}}
      {do
         {$this set_fullness 0.5 0.5}
         {$this
            iterate_frac
            (0.8 {main.drv play_group ok $crowd.play_flags})
            (0.2 {main.drv play_group great $crowd.play_flags})}}}
   {set $crowd.play_flags {'|' kPlayNoBlend kPlayGraphLoop}})
(animate
   ($bad $ok $great $idle $lighter)
   {do
      ($lighter_group lighter_slow)
      ($frac 0)
      {if_else {== [lighter] off}
         {set $lighter 0}
         {set $lighter_group
            {if_else {== [lighter] slow}
               lighter_slow
               lighter_fast}}}
      {set $frac {- 1 $lighter}}
      {'*=' $lighter $great}
      {'*=' $great $frac}
      {$this iterate_frac
         ($bad {main.drv play_group bad $crowd.play_flags})
         ($ok {main.drv play_group ok $crowd.play_flags})
         ($great {main.drv play_group great $crowd.play_flags})
         ($idle {main.drv play_group idle $crowd.play_flags})
         ($lighter {main.drv play_group $lighter_group $crowd.play_flags})}})
(crowd_update
   {if {exists game}
      {$this set show_3d_only FALSE}
      {do
         ($peak_fullness 1)
         {if {== {rnd aspect} kWidescreenAspect}
            {set $peak_fullness 1}}
         {switch [excitement_level]
            (kExcitementBoot {$this set_fullness 0.1 0.1})
            (kExcitementBad {$this set_fullness 0.25 0.25})
            (kExcitementOkay {$this set_fullness 0.5 0.5})
            (kExcitementGreat {$this set_fullness $peak_fullness $peak_fullness})
            (kExcitementPeak {$this set_fullness $peak_fullness $peak_fullness})}}
      {switch [excitement_level]
         (kExcitementBoot {$this animate 1.0 0.0 0.0 0.0 0.0})
         (kExcitementBad {$this animate 0.8 0.2 0.0 0.0 0.0})
         (kExcitementOkay {$this animate 0.0 0.8 0.2 0.0 0.0})
         (kExcitementGreat {$this animate 0.0 0.0 1.0 0.0 1.0})
         (kExcitementPeak {$this animate 0.0 0.0 1.0 0.0 1.0})}})
(excitement
   ($level)
   {set [excitement_level] $level})
(crowd_hide
   {if {exists game}
      {$this set show_3d_only TRUE}})
(crowd_lighters_slow
   {do
      ($lighter [lighter])
      {set [lighter] slow}
      {if {&& {== {world get did_lighter_cam} FALSE} {== $lighter off}}
         {world pick_lighter_shot}}})
(crowd_lighters_fast
   {do
      ($lighter [lighter])
      {set [lighter] fast}
      {if {&& {== {world get did_lighter_cam} FALSE} {== $lighter off}}
         {world pick_lighter_shot}}})
(crowd_lighters_off
   {set [lighter] off}
   {world force_pick_shot})
(crowd_half_tempo
   {$this iterate_frac (1 {main.drv set beat_scale 0.5})})
(crowd_double_tempo
   {$this iterate_frac (1 {main.drv set beat_scale 2})})
(crowd_normal_tempo
   {$this iterate_frac (1 {main.drv set beat_scale 1})})