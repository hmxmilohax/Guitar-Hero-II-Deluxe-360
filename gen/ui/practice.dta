{new
   GHScreen
   practice_selsong_screen
   (panels meta sel_song_panel helpbar)
   (focus sel_song_panel)
   (helpbar
      (default
         ((fret1 help_continue)
            (fret2 help_back)
            (strum help_updown))))
   (sel_song_career FALSE)
   (enter
      {gamecfg set practice_speed 0})
   (SELECT_START_MSG
      {ui goto_screen practice_selpart_screen}
      kDataUnhandled)}
{new
   UIPanel
   selpart_bg_panel
   (file
	{if_else
      $game80s
      selpart_bg_80s.milo
      selpart_bg.milo})}
{new
   GHPanel
   practice_selpart_panel
   (file
	{if_else
      $game80s
      practice_selpart_80s.milo
      practice_selpart.milo})
   (focus part0.btn)
   (sel_buttons part0.btn part1.btn part2.btn)
   (set_button_text
      {do
         ($song
            {gamecfg get_song})
         {part0.btn
            set_text
            {sprint
               "sg_"
               {song_provider get_instrument $song 0}}}
         {part1.btn
            set_text
            {sprint
               "sg_"
               {song_provider get_instrument $song 1}}}
            {part2.btn
               set_text
               "sg_drum"}
         {if
            {==
               {song_provider get_instrument $song 1}
               "rhythm"}
            {part0.btn
               set_text
               "sg_lead"}}
            {part2.btn
               set_state
               kDisabled}
            {part2.btn
               set_showing
               FALSE}
})
   (enter
      {gamecfg set_track_type rhythm}
      {$this set_button_text})
   (SELECT_START_MSG
      {if
         {== $component part0.btn}
         {gamecfg set_track 0 0}}
      {if
         {== $component part1.btn}
         {gamecfg set_track 0 1}}
      {if
         {== $component part2.btn}
         {gamecfg set_track 0 2}
		 {set $roygb TRUE}}
      {ui goto_screen practice_diff_screen})}
{new
   GHScreen
   practice_selpart_screen
   (panels meta practice_selpart_panel helpbar)
   (focus practice_selpart_panel)
   (back_screen practice_selsong_screen)
   (career_mode FALSE)
   (helpbar
      (default
         ((fret1 help_continue)
            (fret2 help_back)
            (strum help_updown))))}
{new
   GHPanel
   sel_diff_practice_panel
   (file
	{if_else
      $game80s
      sel_diff_practice_80s.milo
      sel_diff_practice.milo})
   SEL_DIFFICULTY_COMMON
   (enter
      {$this update_focus})
   (last_difficulty
      {game get_difficulty 0})
   (update_difficulty)}
{new
   GHScreen
   practice_diff_screen
   (panels meta sel_diff_practice_panel helpbar)
   (focus sel_diff_practice_panel)
   (back_screen practice_selpart_screen)
   (helpbar
      (default
         ((fret1 help_continue)
            (fret2 help_back)
            (strum help_updown))))
   (SELECT_START_MSG
      {ui goto_screen practice_sel_section_screen}
      kDataUnhandled)}
{new
   MultiSelectListPanel
   practice_sel_section_panel
   (file
	{if_else
      $game80s
      practice_sel_section_80s.milo
      practice_sel_section.milo})
   (focus sel_section.lst)
   (enter
      {gamecfg
         set
         (practice_sections 0)
         -1}
      {gamecfg
         set
         (practice_sections 1)
         -1}
      {sel_section.lst
         set_provider
         {game practice_section_provider}}
      {title.lbl set_text select_start_section}
      {set
         [section]
         0}
      {meta_music stop}
      {gamecfg set practice_speed 0}
      {$this play_preview})
   (exit
      {song_preview ''}
      {meta_music start}
#ifdef HX_XBOX {profilemgr enable_xmp} #endif)
   (SELECT_START_MSG
      {do
         ($idx
            {-
               {sel_section.lst selected_pos}
               1})
         {if_else
            [section]
            {if_else
               {<
                  $idx
                  {gamecfg
                     get
                     (practice_sections 0)}}
               {handle
                  (ui BAD_SELECT_START_MSG)}
               {do
                  {gamecfg
                     set
                     (practice_sections 1)
                     $idx}
                  {ui goto_screen modifiers_screen_pract}
                  kDataUnhandled}}
            {if_else
               {== $idx -1}
               {do
                  {gamecfg
                     set
                     (practice_sections 0)
                     -1}
                  {gamecfg
                     set
                     (practice_sections 1)
                     -1}
                  {ui goto_screen modifiers_screen_pract}
                  kDataUnhandled}
               {do
                  {gamecfg
                     set
                     (practice_sections 0)
                     $idx}
                  {title.lbl set_text select_end_section}
                  {set
                     [section]
                     1}
                  {section_provider set_start_section $idx}
                  kDataUnhandled}}}})
   (SCROLL_MSG
      {$this play_preview})
   (BUTTON_DOWN_MSG
      {cond
         ({&&
               {== $button kPad_Tri}
               {==
                  [section]
                  0}
               {practice_selpart_screen get career_mode}}
            {career_screen resume_from_practice}
            {ui goto_screen career_screen}
            TRUE)
         ({&&
               {== $button kPad_Tri}
               {==
                  [section]
                  1}}
            {set
               [section]
               0}
            {section_provider set_start_section -1}
            {title.lbl set_text select_start_section}
            TRUE)
         (TRUE kDataUnhandled)})
   (play_preview
      {do
         ($sec
            {-
               {sel_section.lst selected_pos}
               1})
         ($start $end)
         {gamecfg get_section_bounds $sec $start $end}
         {set
            $start
            {- $start 500}}
         {if
            {< $start 0}
            {set $start 0}}
         {song_preview
            {game get_song}
            $start
            $end}})
   (section 0)}
{new
   GHScreen
   practice_sel_section_screen
   (panels meta midi_loader_panel practice_sel_section_panel helpbar)
   (focus practice_sel_section_panel)
   (back_screen practice_diff_screen)
   (career_mode FALSE)
   (helpbar
      (default
         ((fret1 help_continue)
            (fret2 help_back)
            (strum help_updown))))}
{new
   SliderPanel
   modifiers_panel_pract
   (file
	{if_else
      $game80s
      game_modifiers_80s.milo
      game_modifiers.milo})
   (focus gs_guitar.sld)
   (focus_component
      "")
   (reset_focus TRUE)
   (orig_vol 0)
   (finish_load
      {gs_guitar.sld set_num_steps 21}
      {if_else
         {'||'
            {== $modifier_exit multi_vs_selsong_screen}
            {== $modifier_exit multi_coop_selsong_screen}
            {== $modifier_exit multi_fo_selsong_screen}}
         {gs_sfx.sld set_num_steps 31}
         {gs_sfx.sld set_num_steps 41}})
   (slider_changed
      {'||'
         {!=
            {options get band_volume_idx}
            {gs_guitar.sld current}}
         {!=
            {options get guitar_volume_idx}
            {gs_sfx.sld current}}})
   (enter
      {gs_guitar.sld
         set_current
         {int
            {'+'
               0.5
               {/
                  {'+'
                     {- $trackspd 1}
                     0.5}
                  0.1}}}}
      {gs_sfx.sld
         set_current
         {int
            {'+'
               0.5
               {/
                  {'+'
                     {- $speedmod 1}
                     0.5}
                  5.0e-2}}}}
      {stereo.chk set_check $focusmode}
      {$this display_speed_msg $trackspd $speedmod})
   (poll
      {gs_band.sld set_state kDisabled}
      {gs_band.sld set_showing FALSE})
   (reset_gm_title
      {gs_title.lbl set_localized_text GAME_MODIFIER})
   (display_speed_msg
      ($tspd $mspd)
      {gs_title.lbl
         set_localized_text
         {sprintf
            {localize speed_msg}
            {int
               {'+'
                  0.5
                  {'*' $tspd 100}}}
            {int
               {'+'
                  0.5
                  {'*' $mspd 100}}}}})
   (set_speeds
      {if
         {$this slider_changed}
         {do
            {set
               $trackspd
               {-
                  {'+'
                     1
                     {'*'
                        {gs_guitar.sld current}
                        0.1}}
                  0.5}}
            {set
               $speedmod
               {-
                  {'+'
                     1
                     {'*'
                        {gs_sfx.sld current}
                        5.0e-2}}
                  0.5}}
            {$this display_speed_msg $trackspd $speedmod}}})
   (slider_select_cancel
      ($component)
      {switch
         $component
         (gs_guitar.sld
            {$component
               set_current
               [orig_vol]})
         (gs_sfx.sld
            {$component
               set_current
               [orig_vol]})}
      {$this update_help_display}
      {$this set_speeds}
      {$this display_speed_msg $trackspd $speedmod}
      kDataUnhandled)
   (FOCUS_MSG
      {$this
         set
         focus_component
         {$new_focus name}}
      {$this update_help_display})
   (update_help_display
      {if_else
         {$this slider_selected}
         {helpbar
            set_display
            ((fret1 help_confirm)
               (fret2 help_cancel)
               (strum help_adjspd))}
         {do
            {if_else
               {==
                  [focus_component]
                  "gs_stereo.btn"}
               {helpbar
                  set_display
                  ((fret1 help_onoff)
                     (fret2 help_back)
                     (strum help_updown))}
               {helpbar
                  set_display
                  ((fret1 help_select)
                     (fret2 help_back)
                     (strum help_updown))}}}})
   (slider_start_msg
      ($component)
      {do
         {switch
            $component
            (gs_guitar.sld
               {do
                  {play_sfx
                     slider_band
                     (volume 1)
                     (stop TRUE)}
                  {$this set_speeds}})
            (gs_sfx.sld
               {do
                  {play_sfx
                     slider_band
                     (volume 1)
                     (stop TRUE)}
                  {$this set_speeds}})}})
   (SELECT_START_MSG
      {switch
         $component
         (gs_guitar.sld
            {$this
               set
               orig_vol
               {gs_guitar.sld current}})
         (gs_sfx.sld
            {$this
               set
               orig_vol
               {gs_sfx.sld current}})
         (gs_stereo.btn
            {stereo.chk toggle}
            {set
               $focusmode
               {stereo.chk get_check}})
         kDataUnhandled}
      {$this update_help_display}
      {$this set_speeds}
      {$this display_speed_msg $trackspd $speedmod})}
{new
   GHScreen
   modifiers_screen_pract
   (panels meta modifiers_panel_pract helpbar)
   (focus modifiers_panel_pract)
   (allow_back FALSE)
   (BUTTON_DOWN_MSG
      {if
         {&&
            {!
               {modifiers_panel_pract slider_selected}}
            {== $button kPad_Tri}}
         {$this backwards_anim}
         {synth play_sequence button_back.cue}
         {$this set_speeds}
         {ui goto_screen practice_loading_screen}}
      kDataUnhandled)}
{new
   GHPanel
   sel_speed_panel
   (file
	{if_else
      $game80s
      sel_speed_80s.milo
      sel_speed.milo})
   (focus speed0.btn)
   (SELECT_START_MSG
      {switch
         $component
         (speed0.btn
            {gamecfg set practice_speed 0})
         (speed1.btn
            {gamecfg set practice_speed 1})
         (speed2.btn
            {gamecfg set practice_speed 2})
         (speed3.btn
            {gamecfg set practice_speed 3})})}
{new
   GHScreen
   practice_sel_speed_screen
   (panels meta sel_speed_panel helpbar)
   (focus sel_speed_panel)
   (SELECT_START_MSG
      {ui goto_screen practice_loading_screen}
      kDataUnhandled)
   (helpbar
      (default
         ((fret1 help_continue)
            (fret2 help_back)
            (strum help_updown))))}
{new
   GHPanel
   practice_panel
   (file
	{if_else
      $game80s
      practice_panel_80s.milo
      practice_panel.milo})
   (enter
      {events_parser add_sink $this section}
      {game add_sink $this intro_skip}
      {foreach
         $sfx
         (practice_snare practice_kick practice_hat)
         {$sfx add_fader music_fader}})
   (intro_skip
      {set $do_streak TRUE}
      {streak_overlay_panel streak_lastframe} ;should force streak overlay to appear in practice even after a run
      {streak_overlay_panel show_overlay TRUE}
      {mtv_overlay_panel show_overlay TRUE}
      {script_task
         (delay 3)
         (units kTaskSeconds)
         (script
            {mtv_overlay_panel show_overlay FALSE})}
      {set
         [ignore_section]
         {section_provider section_after_last}}
      {if
         {==
            {gamecfg get practice_speed}
            0}
         {beatmatch set_music_speed $speedmod}})
#ifdef HX_EE
   (update
      {click_parser remove_sink $this}
      {if
         {!=
            {gamecfg get practice_speed}
            0}
         {click_parser add_sink $this}}
      {if
         {!
            [keep_section_text]}
         {section.lbl set_text ''}}
      {set
         [keep_section_text]
         FALSE})
#else
(update
      {click_parser remove_sink $this}
      {if
         {&&
            {!=
               {gamecfg get practice_speed}
               0}
            {! $stretch_bg}}
         {click_parser add_sink $this}}
      {beatmatch update_practice_speed}
      {if
         {!
            [keep_section_text]}
         {section.lbl set_text ''}}
      {set
         [keep_section_text]
         FALSE})
#endif
   (exit
      {click_parser remove_sink $this}
      {events_parser remove_sink $this}
      {game remove_sink $this})
   (midi_parser
      ($msg)
      {$this $msg})
   (click_snare
      {practice_snare play})
   (click_kick
      {practice_kick play})
   (click_hat
      {practice_hat play})
   (section
      ($sec)
      {section.lbl
         set_text
         {if_else
            {==
               $sec
               [ignore_section]}
            ''
            $sec}})
   (keep_section_text FALSE)
   (ignore_section '')}
{new
   GHPanel
   practice_focus_panel
   (file practice_panel_focus.milo)
   (enter
      {events_parser add_sink $this section}
      {game add_sink $this intro_skip}
      {foreach
         $sfx
         (practice_snare practice_kick practice_hat)
         {$sfx add_fader music_fader}})
   (intro_skip
      {set $do_streak TRUE}
      {streak_overlay_panel streak_lastframe} ;should force streak overlay to appear in practice even after a run
      {streak_overlay_panel show_overlay TRUE}
      {mtv_overlay_panel show_overlay TRUE}
      {script_task
         (delay 3)
         (units kTaskSeconds)
         (script
            {mtv_overlay_panel show_overlay FALSE})}
      {set
         [ignore_section]
         {section_provider section_after_last}}
      {if
         {==
            {gamecfg get practice_speed}
            0}
         {beatmatch set_music_speed $speedmod}})
#ifdef HX_EE
   (update
      {click_parser remove_sink $this}
      {if
         {!=
            {gamecfg get practice_speed}
            0}
         {click_parser add_sink $this}}
      {if
         {!
            [keep_section_text]}
         {section.lbl set_text ''}}
      {set
         [keep_section_text]
         FALSE})
#else
(update
      {click_parser remove_sink $this}
      {if
         {&&
            {!=
               {gamecfg get practice_speed}
               0}
            {! $stretch_bg}}
         {click_parser add_sink $this}}
      {beatmatch update_practice_speed}
      {if
         {!
            [keep_section_text]}
         {section.lbl set_text ''}}
      {set
         [keep_section_text]
         FALSE})
#endif
   (exit
      {click_parser remove_sink $this}
      {events_parser remove_sink $this}
      {game remove_sink $this})
   (midi_parser
      ($msg)
      {$this $msg})
   (click_snare
      {practice_snare play})
   (click_kick
      {practice_kick play})
   (click_hat
      {practice_hat play})
   (section
      ($sec)
      {section.lbl
         set_text
         {if_else
            {==
               $sec
               [ignore_section]}
            ''
            $sec}})
   (keep_section_text FALSE)
   (ignore_section '')}
#define PRACTICE_PANELS
(practice_panel game hud streak_overlay_panel track_panel mtv_overlay_panel)
#define PRACTICE_FOCUS_PANELS
(practice_focus_panel game hud streak_overlay_panel track_panel mtv_overlay_panel)
{new
   GHScreen
   practice_game_screen
   (panels PRACTICE_PANELS)
   (allow_back FALSE)
   (animate_transition FALSE)
   (in_game TRUE)
   (BUTTON_DOWN_MSG
      {switch
         $button
         (kPad_Select
            {game_restart_fast})
         (kPad_Start
            {if {! {game get game_over}}
               {game set_paused TRUE}
               {ui goto_screen pract_pause_screen}})})
;  (    {if
;         {&&
;            {== $button kPad_Start}
;            {!
;               {game get game_over}}}
;         {game set_paused TRUE}
;         {ui goto_screen pract_pause_screen}})
   (enter
      {practice_panel update})
   (TRANSITION_COMPLETE_MSG
      {if
         $game_unpause_on_enter
         {game set_paused FALSE}
         {set $game_unpause_on_enter FALSE}})
   (poll
      {if
         {game is_missing_controller}
         {game set_paused TRUE}
         {ui goto_screen practice_pause_controller_screen}})}
{new
   GHScreen
   practice_game_focus_screen
   (panels PRACTICE_FOCUS_PANELS)
   (allow_back FALSE)
   (animate_transition FALSE)
   (in_game TRUE)
   (BUTTON_DOWN_MSG
      {switch
         $button
         (kPad_Select
            {game_restart_fast})
         (kPad_Start
            {if {! {game get game_over}}
               {game set_paused TRUE}
               {ui goto_screen pract_pause_focus_screen}})})
   (enter
      {practice_focus_panel update})
   (TRANSITION_COMPLETE_MSG
      {if
         $game_unpause_on_enter
         {game set_paused FALSE}
         {set $game_unpause_on_enter FALSE}})
   (poll
      {if
         {game is_missing_controller}
         {game set_paused TRUE}
         {ui goto_screen practice_pause_controller_screen}})}
{new
   GHPanel
   practice_end_panel
   (file 
   {if_else $gamegh1
   practice_end_gh1.milo
   practice_end.milo})
   (enter
      {hud show_message 0 blank_message}
	  {$this set_focus restart.btn}
      {do
         ($pat
            {localize gems_hit_pattern})
         ($hits
            {player0 gems_hit})
         ($tot
            {'+'
               $hits
               {player0 gems_passed}})
         ($perc
            {int
               {'*'
                  100
                  {/ $hits $tot}}})
         {stats.lbl
            set_localized_text
            {sprintf $pat $perc $hits $tot}}}
      {do
         ($career
            {practice_selpart_screen get career_mode})
         {pause_buttons_career.view set_showing $career}
         {pause_buttons.view
            set_showing
            {! $career}}
         {$this
            set_focus
            {if_else $career restart_career.btn restart.btn}}}
#ifdef HX_XBOX
{if
         {campaign
            check_practice_achievement
            {gamecfg get_song}}
         {gamecfg set allow_achievements TRUE}
         {achievements submit practice}
         {gamecfg set allow_achievements FALSE}}
#endif)
   (SELECT_START_MSG
      {switch
         $component
         (restart.btn
            {game_restart})
         (restart_career.btn
            {game_restart})
         (change_speed.btn
            {modifiers_screen_pract set back_screen pract_pause_screen}
            {meta_loading_nosave_goto modifiers_screen_pract})
         (change_speed_career.btn
            {modifiers_screen_pract set back_screen pract_pause_screen}
            {meta_loading_nosave_goto modifiers_screen_pract})
         (new_sec.btn
            {meta_loading_nosave_goto practice_sel_section_screen})
         (new_sec_career.btn
            {meta_loading_nosave_goto practice_sel_section_screen})
         (new_song.btn
            {meta_loading_nosave_goto practice_selsong_screen})
         (quit.btn
               {meta_loading_nosave_goto main_screen})
         (quit_career.btn
            {career_screen resume_from_practice}
            {meta_loading_goto career_screen})})}
{new
   GHScreen
   practice_end_screen
   (panels PRACTICE_PANELS practice_end_panel helpbar)
   (focus practice_end_panel)
   (clear_vram TRUE)
   (allow_back FALSE)
   (animate_transition FALSE)
   (in_game TRUE)
   (helpbar
      (default
         ((fret1 help_continue)
            (strum help_updown))))}
{new
   GHScreen
   practice_end_focus_screen
   (panels PRACTICE_FOCUS_PANELS practice_end_panel helpbar)
   (focus practice_end_panel)
   (clear_vram TRUE)
   (allow_back FALSE)
   (animate_transition FALSE)
   (in_game TRUE)
   (helpbar
      (default
         ((fret1 help_continue)
            (strum help_updown))))}
{new
   GHScreen
   practice_change_speed_screen
   (panels PRACTICE_PANELS sel_speed_panel helpbar)
   (focus sel_speed_panel)
   (animate_transition FALSE)
   (in_game TRUE)
   (back_screen
      "")
   (helpbar
      (default
         ((fret1 help_continue)
            (fret2 help_back)
            (strum help_updown))))
(enter
      {mtv_overlay_panel set_showing FALSE})
   (SELECT_START_MSG
      {game_restart}
      kDataUnhandled)
   (BUTTON_DOWN_MSG
      {if
         {== $button kPad_Tri}
         {ui
            goto_screen
            [back_screen]}}
      kDataUnhandled)}
{func
   practice_restart
   {if_else
      $focusmode
      {meta_loading_nosave_goto practice_game_focus_screen}
      {meta_loading_nosave_goto practice_game_screen}}}