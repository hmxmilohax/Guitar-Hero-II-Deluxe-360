; Chose what main menu to display based off egg screen. Custom Edition and standard gh2dx screen are swapped based on elf usage.
;main.milo - GH2DX | main_egg0.milo - GH2DXCE | main_egg1.milo - GH1 | main_egg2.milo - GH80SDX | main_egg3.milo - GH3 | main_egg_final.milo - ???????
{new
   GHPanel
   main_panel
	(file
		{if_else $egg5 main_egg_final.milo
			{if_else $egg4 main_egg3.milo
				{if_else $egg3 main_egg2.milo
					{if_else $egg2 main_egg1.milo
						{if_else $egg1 #ifndef _ONYX main_egg0.milo #else main.milo #endif
							{if_else $game80s main_egg2.milo #ifndef _ONYX main.milo #else main_egg0.milo #endif}}}}}})
   (focus main_quickspin.btn)
   (msg_last_frame 0)
   (already_entered FALSE)
   (memcard_flow_entered FALSE)
   (retain_last_focus FALSE)
   (enter
      {campaign set_profile_slot 0}
      {set $main_entered TRUE}
      {set $cheat_enabled_unlock_all 1}
      {campaign set_all_access 1}
      {$this reset_player_settings}
      {if
         {!
            [already_entered]}
         {synth play_sequence meta_lick}
         {$this set already_entered TRUE}}
      {game set_tutorial_running FALSE}
      {main_msg.view set_showing FALSE}
      {set
         [msg_last_frame]
         0}
	  {gamecfg set_player 0 {profilemgr active_padnum}}
      {content_mgr start_enumeration}
	  )
   (SELECT_START_MSG
      {switch
         $component
         (main_career.btn
            ;{ui goto_screen nameprof_screen}
			)
		 (main_quickspin.btn
            {gamecfg set mode career}
            {ui goto_screen sel_song_screen})
         (main_multiplayer.btn
            {ui goto_screen multi_controller_select_screen})
         (main_tutorial.btn
		    {gamecfg set mode practice}
            {gamecfg set practice_speed 0}
            {ui goto_screen sel_song_screen})
         (main_leaderboards.btn
            {ui goto_screen lag_screen})
	     (main_options.btn
            {ui goto_screen credits_screen})})
   (poll
      {if_else
         {game is_missing_multi_controller}
         {$this disable main_multiplayer.btn}
         {$this enable main_multiplayer.btn}}
      {main_msg.view
         set_showing
         {<
            {taskmgr ui_seconds}
            {$this get msg_last_frame}}})
   (reset_player_settings
      {{game get_player_config 0}
         set_difficulty
         kDifficultyExpert}
      {game set_guitar_index 1}
      {do
         ($player2_config
            {game get_player_config 1})
         {$player2_config set_character rockabill1 TRUE}
         {$player2_config set_guitar_index 0}
         {$player2_config set_difficulty kDifficultyMedium}})
   (display_cheat_msg
      ($cheat $enable)
      {mm_msg.lbl
         set_localized_text
         {sprintf
            {localize
               {if_else $enable cheat_enabled_fmt cheat_disabled_fmt}}
            {localize $cheat}}}
      {$this
         set
         msg_last_frame
         {'+'
            {taskmgr ui_seconds}
            5}})}
{new
   GHScreen
   main_screen
   (panels meta main_panel helpbar)
   (focus main_panel)
   (helpbar
      (default
         ((fret1 help_continue)
            (strum help_updown))))
   (access_all
      {main_panel reset_player_settings})
   (enter
      {$this turn_off_ambient})}