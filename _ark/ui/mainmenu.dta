{new GHPanel unlock_pfo_panel
   (file unlock_profaceoff.milo)
   (enter
      {we_tape1.mesh set_showing FALSE}
      {we_skull.tex set_bitmap track/custom_track_textures/logo_keep.png}
      {we_skulls.mesh set_local_scale 3.3 3 3}
      {we_skulls.mesh set_local_pos_index 0 140}
      {we_skulls.mesh set_local_pos_index 2 -35}
      {we_deaththreat.lbl set_local_pos_index 0 -195}
      {we_deaththreat.lbl set_local_scale 1.3 1.3 1.3}
      {we_quote.lbl set_local_pos_index 0 -195}
      {we_quote.lbl set_local_pos_index 2 -35}
      {we_quote.lbl set_local_scale 1.3 1.3 1.3}
      {we_bandname.lbl set_localized_text {campaign profile_name 0}}
   )
}
{new GHScreen unlock_pfo_screen
   (panels meta unlock_pfo_panel helpbar)
   (animate_transition FALSE)
   (focus unlock_pfo_panel)
   (helpbar
      (default
         ((fret1 help_continue))))
   (BUTTON_DOWN_MSG
      {if {== $button kPad_X}
         {ui goto_screen main_screen}})}
; Chose what main menu to display based off egg screen. Custom Edition and standard gh2dx screen are swapped based on elf usage.
;main.milo - GH2DX | main_egg0.milo - GH2DXCE | main_egg1.milo - GH1 | main_egg2.milo - GH80SDX | main_egg3.milo - GH3 | main_egg_final.milo - gold
{new GHPanel main_panel ;panels come before screens in milo
    (file
       {if_else {== $menutheme gh3}
          main_egg3.milo
          main.milo
       }
    )
   (focus main_quickspin.btn) ;focus on quickplay button on enter
   (msg_last_frame 0) ;cheat messages no longer occur in DX
   (already_entered FALSE)
   (memcard_flow_entered FALSE)
   (retain_last_focus FALSE)
   (rocks360_theme
      {do
         {mainmenu.tex set_bitmap "track/custom_track_textures/main_rt360_keep.png"}
         {main set normal_color {pack_color 0.03 0.30 0.01}}
         {main_buttons.view set_local_pos 0 0 -90}
      }
   )
   (gh80s_theme
      {do
         {mainmenu.tex set_bitmap "track/custom_track_textures/main_80s_keep.png"}
         {main set normal_color {pack_color 0.23 0.51 0.69}}
         {main set focus_color {pack_color 0.84 0.20 0.57}}
         {main set selecting_color {pack_color 0 0.92 0.92}}
         {main_buttons.view set_local_pos 0 0 -100}
      }
   )
   (gh1_theme
      {do
         {mainmenu.tex set_bitmap "track/custom_track_textures/mainmenu_gh1_keep.png"}
         {main set normal_color {pack_color 0.81 0.81 0.81}}
         {main set focus_color {pack_color .81 0.69 0.03}}
         {main_career.btn set_type impact}
         {main_quickspin.btn set_type impact}
         {main_multiplayer.btn set_type impact}
         {main_tutorial.btn set_type impact}
         {main_leaderboards.btn set_type impact}
         {main_options.btn set_type impact}
         {main_buttons.view set_local_pos 0 0 -160}
      }
   )
   (gold_theme
      {do
         {main_buttons.view set_local_pos 0 0 -115}
         {mainmenu.tex set_bitmap "track/custom_track_textures/mainmenu_nohand_gold_keep.png"}
         {main set normal_color {pack_color 0.74 0.57 0.07}}
      }
   )
   (custom_theme
      {do
         {main_buttons.view set_local_pos 0 0 -115}
         {mainmenu.tex set_bitmap "track/custom_track_textures/main_custom_keep.png"}
         {main set normal_color {pack_color 0.43 0.23 0.19}}
      }
   )
   (enter
       {campaign buy_item speedtest9 0}
         {switch
            $menutheme
            (gold {$this gold_theme})
            (custom {$this custom_theme})
            (gh1 {$this gh1_theme})
            (80s {$this gh80s_theme})
            (rocks360 {$this rocks360_theme})
         }
      {if $newsave
         {set $newsave FALSE}
         {ui goto_screen unlock_pfo_screen}
      }
      ;{set $hardware_check FALSE}
      ;{main_screen save_objects "game:/hardware_check.milo"} ;streak meter only does not cause stutter when gameplay is at high fps, like in emulator. this ;creates a file we check for later to run the streak meter or not
      ;{if {load_objects "game:/hardware_check.milo"}
         ;{set $cheat_enabled_unlock_all 1}
         ;{campaign set_all_access 1}
         ;{set $disable_save 1}
      ;}
      ;{{load_objects "game:/hardware_check.milo"} save_objects "game:/converted.milo"}
      {campaign set_profile_slot 0} ;attempt to set up career for quickplay
      {set $main_entered TRUE} ;this lets the game know first boot has occured and no longer needs to perform additional setup when main screen is entered again
      ;{set $cheat_enabled_unlock_all 1} ;unlock all for dx
      ;{campaign set_all_access 1} ;unlock all for dx
      {$this reset_player_settings} ;sets player defaults, only run onces on first entry
      {set $dontreset TRUE} ;monkey script to make the player reset not occur after first entry
      {if {! [already_entered]} {synth play_sequence meta_lick} {$this set already_entered TRUE}} ;play a noise on first entry, and tell the game entry has occured
      {game set_tutorial_running FALSE} ;ayo fuck tutorials
      {main_msg.view set_showing FALSE} ;this is the group of objects and text for vanilla cheat notifications
      {set [msg_last_frame] 0} ;resets the timeline for the cheat notification for later playback (unused)
      {gamecfg set_player 0 {profilemgr active_padnum}} ;set player 1 to the current active gamepad on xbox 260
      {content_mgr start_enumeration} ;start checking for dlc on xbox
      )
   (SELECT_START_MSG ;what to do when a button is pressed or a menu item is selected
      {switch $component ;switch action based on result of which button is selected
         (main_career.btn
            {gamecfg set mode career}
            {if_else
               {>
                  {campaign num_profiles}
                  0}
               {ui goto_screen manage_band_screen}
               {do
                  {nameprof_screen set back_screen main_screen}
                  {nameprof_screen set is_editing FALSE}
                  {nameprof_screen set profile_slot 0}
                  {nameprof_screen set next_screen main_screen}
                  {ui goto_screen nameprof_screen}}}
            ;{game set_song {campaign pick_attract_song}} {game set_career} {ui goto_screen loading_screen}
            ;{{load_objects "game:/test.milo"} save_objects "game:/converted.milo"} ;test code for milo conversion. input milo must be named as test.milo_xbox
            ;{ui goto_screen nameprof_screen} ;name profile screen for highscores (career)
            )
         (main_quickspin.btn
            {gamecfg set mode career} ;set to career so select song screen shows saved scores
            {set $gh3list {random_int 1 4}} ;set the random number for one of the 4 combinations of gh3 song list screen
            {ui goto_screen sel_song_screen} ;go to the select song screen
            kDataUnhandled)
         (main_multiplayer.btn
            {set $gh3list {random_int 1 5}}
            {ui goto_screen multi_controller_select_screen} ;go to the controller configuration screen for multiplayer (xbox only)
            kDataUnhandled)
         (main_tutorial.btn
            {gamecfg set mode practice} ;set the current game mode to practice
            {gamecfg set practice_speed 0} ;we use our own speed scripting, so ensure practice speed is set to full speed
            {set $gh3list {random_int 1 4}} ;set the random number for one of the 4 combinations of gh3 song list screen
            {ui goto_screen sel_song_screen} ;go to the select song screen
            kDataUnhandled)
         (main_leaderboards.btn
            {ui goto_screen lag_screen} ;go directly to the calibration screen
            kDataUnhandled)
         (main_options.btn
            {ui goto_screen credits_screen} ;go directly to the credits screen
            kDataUnhandled)})
   (poll ;poll runs code in the background to perform actions based on parameters
      {if_else {game is_missing_multi_controller} ;checks if a player 2 controller is plugged in
         {$this disable main_multiplayer.btn} ;enables multiplayer to be selected if player 2 is plugged in
         {$this enable main_multiplayer.btn} ;disables multiplayer to be selected if player 2 is plugged in
         }
      {main_msg.view set_showing {< {taskmgr ui_seconds} ;if the cheat display message has been showing for the desired amount of time, hide it
            {$this get msg_last_frame} ;and reset the current point in the animation timeline
            }}
            )
   (reset_player_settings ;runs on entry to main screen
   {if {! $dontreset} ;monkey script to make sure the contents only run the first time main screen is entered
      {set $songtitle TRUE} ;default song title always on as there is a ton of information in mtv overlay
      {{game get_player_config 0} set_difficulty kDifficultyExpert} ;default player 1 to expert
      {game set_guitar_index 1} ;set the default guitar to the red sg
      {do ($player2_config {game get_player_config 1}) ;set up player 2 variable for use in the next lines
         {$player2_config set_character rockabill1 TRUE} ;set player 2 to eddie knox by default
         {$player2_config set_guitar_index 0} ;set default guitar for player 2 to the les paul
         {$player2_config set_difficulty kDifficultyExpert} ;set default difficulty for player 2 to expert
         }})
   (display_cheat_msg ;vanilla cheat display popup, now completely unused in DX
      ($cheat $enable) ;set up local variables
      {mm_msg.lbl set_localized_text ;set the cheat label text to the following
         {sprintf ;combine functions
            {localize {if_else $enable cheat_enabled_fmt cheat_disabled_fmt}} ;if a chect is being enabled, set the text to enabled, if its being disabled, set the text to disabled
            {localize $cheat} ;grab the locale for the currently called cheat and add it to the label
            }}
      {$this set msg_last_frame {'+' {taskmgr ui_seconds} 5}} ;play the animation of the cheat popup for 5 seconds, then reset via (poll)
    )
}
{new GHScreen main_screen
   (panels meta main_panel helpbar) ;what panels are included in this screen, in draw order (help bar is always on top)
   (focus main_panel) ;which panel to set focus to
   (helpbar ;setup helpbar
      (default ((fret1 help_continue) (strum help_updown))) ;we only need continue and up down for help
   )
   (access_all {main_panel reset_player_settings}) ;reset the player settings when debug function is called
   (enter {$this turn_off_ambient}) ;while we play metaloops, don't play ambient sfx
}