#include game_track_panel.dta
#include game_hud_panel.dta
#define DRUMMER_EXPLODE_DELAY
(3.0)
#define MTV_OVERLAY_PANEL_HANDLERS
(
   (file mtv_overlay.milo)
   (enter ;when the screen is entered
      {mtv_campaign_line1.lbl set color MTV_OVERLAY_COLOR} ;custom mtv overlay color
      {mtv_campaign_line2.lbl set color MTV_OVERLAY_COLOR} ;custom mtv overlay color
      {mtv_campaign_line3.lbl set color MTV_OVERLAY_COLOR} ;custom mtv overlay color
      {mtv_overlay.view set_local_scale .5 1 .5} ;make the mtv overlay small
      {mtv_overlay.view set_local_pos -160 1 85} ;move the mtv overlay to the top left corner of the screen, was -240 1 85
;     {$this setup_text} ;run the setup_text code block below
   )
   (setup_text ;called when mtv screen is entered, as well as several other places in the hud panel to refresh stats defined below
      {do
         ($gems_hit {player0 gems_hit}) ;setup gems hit variable
         ($gems_total {'+' $gems_hit {player0 gems_passed}}) ;setup total gems that have passed the strikeline variable
         ($song {gamecfg get_song}) ;setup song variable
         ($song_text {game get_song_text}) ;setup song text variable
         ($artist_text {game get_song_artist_text}) ;setup artist variable
         {do
            ($prefix "mtv_campaign_line") ;setup prefix variable to switch between line numbers
            {mtv_campaign_song_id.view set_showing TRUE} ;show the mtv overlay
            {if_else {!= $speedmod 1} ;if song speed is set to anything other than 100%
               {if_else {&& {== {gamecfg get mode} practice} {!= {gamecfg get practice_speed} 0}} ;now check if the game mode is practice and the practice speed is set to full speed
                  {$this set_line $prefix 1
                     $song_text ;if all speeds are 100% simply show the song name on line 1
                  }
                  {$this set_line $prefix 1
                     {sprint
                        $song_text
                        " ("
                        {int {'+' 0.5 {'*' $speedmod 100}}} ;if song speed is not 100%, do math to report the song speed as a percentage on line 1
                        "%)"
                     }
                  }
               }
               {$this set_line $prefix 1 $song_text} ;if all speeds are 100% simply show the song name on line 1
            }
            {$this set_line $prefix 2
               {sprint
                  {localize performertext}
                  $artist_text ;the artist text line is always just "BY - ARTIST"
               }
            }
            {if_else {&& {! $cheat_enabled_performance_mode} $autoplay} ;check for autoplay enabled without author variable, the author checks will override this if needed
               {set $mtvline3
                  {sprint
                     "\n"
                     {localize autoplaytext}
                  }
               }
               {set $mtvline3
                  {sprint "\n"}
               }
            }
            {if {!= "" {localize {sprint {gamecfg get_song} "_author"}}} ; If not debug, check for author string in locale
               {if_else {&& {! $cheat_enabled_performance_mode} $autoplay} ;check for autoplay enabled and author present in locale
                  {set $mtvline3
                     {sprint
                        "\n"
                        {localize authortext}
                        ": "
                        {localize
                           {sprint $song "_author"}
                        }
                        "\n"
                        {localize autoplaytext} ;apparently we weren't showing author in autoplay, fixed 
                     }
                  }
                  {set $mtvline3
                     {sprint
                        "\n"
                        {localize authortext} ; $mtvline3 will hold everything that only needs to be set one time, before the song
                        ": "
                        {localize
                           {sprint $song "_author"}
                        }
                        "\n"
                     }
                  }
               }
            }
            {if_else $author ;check for author variable in DLC contexts.dta
               {if_else {&& {! $cheat_enabled_performance_mode} $autoplay} ;check for autoplay enabled with author variable
                  {set $mtvline3
                     {sprint
                        "\n"
                        {localize authortext}
                        ": "
                        $author
                        "\n"
                        {localize autoplaytext} ;apparently we weren't showing author in autoplay, fixed
                     }
                  }
                  {set $mtvline3
                     {sprint
                        "\n"
                        {localize authortext} ; $mtvline3 will hold everything that only needs to be set one time, before the song
                        ": "
                        $author
                        "\n"
                     }
                  }
               }
            }
            {if_else {|| $cleanmtv $autoplay {== {gamecfg get mode} multi_coop}} ;check for autoplay to determine whether to do the missed/overtstrum/percentage stuff
               {$this set_line $prefix 3
                  {sprint $mtvline3} ;if autoplay, just put the static shit
               }
               {$this set_line $prefix 3
                  {sprint
                     $mtvline3 ;if not autoplay, first put the stuff we pregenerated before
                     $notesmissed
                     {localize missedtext}
                     $overstrums
                     {localize overstrumstext}
                     "\n100.00%" ;then apply the notes missed and overstum variables we set in streak panel sinks, and new line, hardcode 100% since this is setup
                  }
               }
            }
         }
      }
   )
   (update_text ;called from hud_panel to reduce scripting to run on each note hit
      {do
         ($gems_hit {player0 gems_hit}) ;setup gems hit variable
         ($gems_total {'+' $gems_hit {player0 gems_passed}}) ;setup total gems that have passed the strikeline variable
         {$this set_line "mtv_campaign_line" 3
            {sprint
               $mtvline3 ;if not autoplay, first put the stuff we pregenerated before
               $notesmissed
               {localize missedtext}
               $overstrums
               {localize overstrumstext}
               "\n" ;then apply the notes missed and overstum variables we set in streak panel sinks, and new line
               {sprint
                  {*
                     {/
                        {if_else $beforefirstnote_p0
                           1
                           $gems_hit
                        }
                        {if_else $beforefirstnote_p0
                           1
                           $gems_total
                        }
                     }
                  100
                  }
               "%"
               }
            } ;do some math to report the current notes hit out of notes passed as a percentage
         }
      }
   )
   (set_line ;called in mtv overlay to apply the text to two labels at once
      ($prefix $i $text) ;setup variables
      {{sprint $prefix $i ".lbl"} set_localized_text $text} ;set the main text objects
      {{sprint $prefix $i "_shadow.lbl"} set_localized_text $text} ;set the shadow text objects
   )
   (show_overlay ;code to run to show the overlay or not based on a variable
      ($show) ;setup variable
      {$this set_showing $show} ;show or hide based off variable
      {$this set_paused FALSE} ;we refresh the mtv overlay alot, so we never want it paused
   )
)
{new
   GHPanel
   mtv_overlay_panel
   MTV_OVERLAY_PANEL_HANDLERS
}
{new
   GHPanel
   mtv_overlay_panel_buffer
   MTV_OVERLAY_PANEL_HANDLERS
}
{new GHPanel name_overlay_panel
   (file name_overlay.milo)
   (enter ;when the screen is entered
      {mtv_overlay.view set_local_scale .7 1 .7} ;make the mtv overlay small
      {mtv_overlay.view set_local_pos 186 1 -345} ;move the mtv overlay to the top left corner of the screen, was -240 1 85
;     {$this setup_text} ;run the setup_text code block below
   )
   (setup_text ;called when mtv screen is entered, as well as several other places in the hud panel to refresh stats defined below
      {do
         ($prefix "mtv_campaign_line") ;setup prefix variable to switch between line numbers
         {mtv_campaign_song_id.view set_showing TRUE} ;show the mtv overlay
         {if_else $autoplay ;check for autoplay enabled without author variable, the author checks will override this if needed
            {set $mtvline1
               {localize autoplaytext}
            }
            {set $mtvline1
               {campaign profile_name 0}
            }
         }
         {$this set_line $prefix 1
            $mtvline1
         }
         {$this set_line $prefix 2
            {sprint
               "\n\n\n\n\n" ;we dont use this line so erase it
            }
         }
         {$this set_line $prefix 3
            {sprint
               "\n\n\n\n\n" ;we dont use this line so erase it
            }
         }
      }
   )
    (set_debug_line ;for debug use, usage: {name_overlay_panel set_debug_line {sprint "whatever you want" $includingvars "will work"}}
      ($text)
      {mtv_campaign_line1.lbl set_showing FALSE}
      {mtv_campaign_line1_shadow.lbl set_showing FALSE}
      {mtv_campaign_line1.lbl set_type postertype}
      {mtv_campaign_line1_shadow.lbl set_type postertype}
      {mtv_campaign_line1.lbl set_localized_text $text}
      {mtv_campaign_line1_shadow.lbl set_localized_text $text}
      {mtv_campaign_line1.lbl set_showing TRUE}
      {mtv_campaign_line1_shadow.lbl set_showing TRUE}
    )
    (set_line ;called in mtv overlay to apply the text to two labels at once
      ($prefix $i $text) ;setup variables
      {{sprint $prefix $i ".lbl"} set color {pack_color $namecolor_r $namecolor_g $namecolor_b}}
      {{sprint $prefix $i ".lbl"} set_type $namefont}
      {{sprint $prefix $i "_shadow.lbl"} set_type $namefont}
      {{sprint $prefix $i ".lbl"} set_localized_text $text} ;set the main text objects
      {{sprint $prefix $i ".lbl"} set alignment kMiddleCenter}
      {{sprint $prefix $i ".lbl"} set width 640.0}
      {{sprint $prefix $i "_shadow.lbl"} set_localized_text $text} ;set the shadow text objects
      {{sprint $prefix $i "_shadow.lbl"} set alignment kMiddleCenter}
      {{sprint $prefix $i "_shadow.lbl"} set width 640.0}
      {mtv_campaign_line2.lbl set_showing FALSE}
      {mtv_campaign_line2_shadow.lbl set_showing FALSE}
      {mtv_campaign_line3.lbl set_showing FALSE}
      {mtv_campaign_line3_shadow.lbl set_showing FALSE}
   )
   (show_overlay ;code to run to show the overlay or not based on a variable
      ($show) ;setup variable
      {$this set_showing $show} ;show or hide based off variable
      {$this set_paused FALSE} ;we refresh the mtv overlay alot, so we never want it paused
   )
}
#define POORSPY_RESET_HANDLER
(
   {if {exists slot_duration_task} ;check if the below task is running
      {delete slot_duration_task} ;delete the task if already running
   }
   {set $slot_duration_0 0}
   {set $slotstate_count_0 0}
   {set $slot_duration_1 0}
   {set $slotstate_count_1 0}
   {set $slot_duration_2 0}
   {set $slotstate_count_2 0}
   {set $slot_duration_3 0}
   {set $slotstate_count_3 0}
   {set $slot_duration_4 0}
   {set $slotstate_count_4 0}
   {set $slot_duration_5 0}
   {set $slotstate_count_5 0}
   {set $slot_duration_6 0}
   {set $slotstate_count_6 0}
   {poorspy_image_panel poorspy_lbl_update}
   {poorspy_image_panel_buffer poorspy_lbl_update}
)
#define POORSPY_PANEL_HANDLER
(
   (file poorspy_viewer.milo) ;milo file used for the panel. found in /ui/gen/
   (poorspy_lbl_update ;for debug use, usage: {name_overlay_panel poorspy_lbl_update {sprint "whatever you want" $includingvars "will work"}}
      {if_else $autoplay
         kDataUnhandled
         {do
            {ss_button_0.lbl set_text {sprint $slotstate_count_0 "\n" $slot_duration_0}}
            {ss_button_0_shadow.lbl set_text {sprint $slotstate_count_0 "\n" $slot_duration_0}}
            {gs_tile0.mat set alpha {if_else $slotstate_0 $poorspyon $poorspyoff}}
            {ss_button_1.lbl set_text {sprint $slotstate_count_1 "\n" $slot_duration_1}}
            {ss_button_1_shadow.lbl set_text {sprint $slotstate_count_1 "\n" $slot_duration_1}}
            {gs_tile1.mat set alpha {if_else $slotstate_1 $poorspyon $poorspyoff}}
            {ss_button_2.lbl set_text {sprint $slotstate_count_2 "\n" $slot_duration_2}}
            {ss_button_2_shadow.lbl set_text {sprint $slotstate_count_2 "\n" $slot_duration_2}}
            {gs_tile2.mat set alpha {if_else $slotstate_2 $poorspyon $poorspyoff}}
            {ss_button_3.lbl set_text {sprint $slotstate_count_3 "\n" $slot_duration_3}}
            {ss_button_3_shadow.lbl set_text {sprint $slotstate_count_3 "\n" $slot_duration_3}}
            {gs_tile3.mat set alpha {if_else $slotstate_3 $poorspyon $poorspyoff}}
            {ss_button_4.lbl set_text {sprint $slotstate_count_4 "\n" $slot_duration_4}}
            {ss_button_4_shadow.lbl set_text {sprint $slotstate_count_4 "\n" $slot_duration_4}}
            {gs_tile4.mat set alpha {if_else $slotstate_4 $poorspyon $poorspyoff}}
            {ss_button_5.lbl set_text {sprint $slotstate_count_5 "\n" $slot_duration_5}}
            {ss_button_5_shadow.lbl set_text {sprint $slotstate_count_5 "\n" $slot_duration_5}}
            {gs_tile5.mat set alpha {if_else $slotstate_5 $poorspyon $poorspyoff}}
            {ss_button_6.lbl set_text {sprint $slotstate_count_6 "\n" $slot_duration_6}}
            {ss_button_6_shadow.lbl set_text {sprint $slotstate_count_6 "\n" $slot_duration_6}}
            {gs_tile6.mat set alpha {if_else $slotstate_6 $poorspyon $poorspyoff}}

         }
      }
   )
   (enter ;when the panel is entered
      {set $poorspyon 0.75}
      {set $poorspyoff 0.25}
      {set $scorespyupdatesec {if_else $focusmode 0.026 0.051}}
      {$this iterate UILabel $l
         {$l set color {pack_color 1 1 1}} ;set all label colors to white
      }
      {$this iterate UILabel $l
         {$l set_type postertype} ;set all label colors to white
      }
      {$this iterate UILabel $l
         {$l set width 30} ;set the width of all labels
      }
      {$this iterate UILabel $l
         {$l set height 30} ;set the height of all labels
      }
      {ss_button_0_shadow.lbl set color {pack_color 0 0 0}} ;then set all shadow labels to black
      {ss_button_1_shadow.lbl set color {pack_color 0 0 0}}
      {ss_button_2_shadow.lbl set color {pack_color 0 0 0}}
      {ss_button_3_shadow.lbl set color {pack_color 0 0 0}}
      {ss_button_4_shadow.lbl set color {pack_color 0 0 0}}
      {ss_button_5_shadow.lbl set color {pack_color 0 0 0}}
      {ss_button_6_shadow.lbl set color {pack_color 0 0 0}}
      {if {== {gamecfg get mode} career} ;only reset poorspy if in career (practice lingers inputs)
         POORSPY_RESET_HANDLER
      }
      {gs_background.view set_local_pos_index 0 -100} ;move the group as a whole
      {gs_background.view set_local_pos_index 2 -85} ;move the group as a whole
      {set $sstexty 215} ;set an anchor point for the text
      {set $sstextx -100} ;set an anchor point for the text
      {ss_button_0.lbl set_local_pos_index 0 $sstextx} ;move all of the labels into place
      {ss_button_1.lbl set_local_pos_index 0 {+ $sstextx 44}}
      {ss_button_2.lbl set_local_pos_index 0 {+ $sstextx 88}}
      {ss_button_3.lbl set_local_pos_index 0 {+ $sstextx 132}}
      {ss_button_4.lbl set_local_pos_index 0 {+ $sstextx 176}}
      {ss_button_5.lbl set_local_pos_index 0 {+ $sstextx 230}}
      {ss_button_6.lbl set_local_pos_index 0 {+ $sstextx 285}}
      {ss_button_0.lbl set_local_pos_index 2 $sstexty}
      {ss_button_1.lbl set_local_pos_index 2 $sstexty}
      {ss_button_2.lbl set_local_pos_index 2 $sstexty}
      {ss_button_3.lbl set_local_pos_index 2 $sstexty}
      {ss_button_4.lbl set_local_pos_index 2 $sstexty}
      {ss_button_5.lbl set_local_pos_index 2 $sstexty}
      {ss_button_6.lbl set_local_pos_index 2 $sstexty}
      {set $sstextshadowoffset 1} ;set shadow offset anchor
      {ss_button_0_shadow.lbl set_local_pos_index 0 {+ $sstextx $sstextshadowoffset}} ;move all of the shadow labels into place
      {ss_button_1_shadow.lbl set_local_pos_index 0 {+ {+ $sstextx 44} $sstextshadowoffset}}
      {ss_button_2_shadow.lbl set_local_pos_index 0 {+ {+ $sstextx 88} $sstextshadowoffset}}
      {ss_button_3_shadow.lbl set_local_pos_index 0 {+ {+ $sstextx 132} $sstextshadowoffset}}
      {ss_button_4_shadow.lbl set_local_pos_index 0 {+ {+ $sstextx 176} $sstextshadowoffset}}
      {ss_button_5_shadow.lbl set_local_pos_index 0 {+ {+ $sstextx 230} $sstextshadowoffset}}
      {ss_button_6_shadow.lbl set_local_pos_index 0 {+ {+ $sstextx 285} $sstextshadowoffset}}
      {ss_button_0_shadow.lbl set_local_pos_index 2 {- $sstexty $sstextshadowoffset}}
      {ss_button_1_shadow.lbl set_local_pos_index 2 {- $sstexty $sstextshadowoffset}}
      {ss_button_2_shadow.lbl set_local_pos_index 2 {- $sstexty $sstextshadowoffset}}
      {ss_button_3_shadow.lbl set_local_pos_index 2 {- $sstexty $sstextshadowoffset}}
      {ss_button_4_shadow.lbl set_local_pos_index 2 {- $sstexty $sstextshadowoffset}}
      {ss_button_5_shadow.lbl set_local_pos_index 2 {- $sstexty $sstextshadowoffset}}
      {ss_button_6_shadow.lbl set_local_pos_index 2 {- $sstexty $sstextshadowoffset}}
      {gs_tile0.mesh set_local_scale .25 .25 .25} ;shrink the fret images
      {gs_tile1.mesh set_local_scale .25 .25 .25}
      {gs_tile2.mesh set_local_scale .25 .25 .25}
      {gs_tile3.mesh set_local_scale .25 .25 .25}
      {gs_tile4.mesh set_local_scale .25 .25 .25}
      {gs_tile5.mesh set_local_scale .25 .25 .25}
      {gs_tile6.mesh set_local_scale .25 .25 .25}
      {gs_tile0.mesh set_local_pos_index 0 0} ;move the fret images into place
      {gs_tile1.mesh set_local_pos_index 0 20}
      {gs_tile2.mesh set_local_pos_index 0 40}
      {gs_tile3.mesh set_local_pos_index 0 60}
      {gs_tile4.mesh set_local_pos_index 0 80}
      {gs_tile5.mesh set_local_pos_index 0 105}
      {gs_tile6.mesh set_local_pos_index 0 130}
      {gs_tile0.mesh set_showing TRUE} ;force show all fret images
      {gs_tile1.mesh set_showing TRUE}
      {gs_tile2.mesh set_showing TRUE}
      {gs_tile3.mesh set_showing TRUE}
      {gs_tile4.mesh set_showing TRUE}
      {gs_tile5.mesh set_showing TRUE}
      {gs_tile6.mesh set_showing TRUE}
      {ss_button_0.tex set_bitmap {sprint "track/custom_track_textures/ss_button_" $poorspybuttontype "_keep.png"}}
      {ss_button_5.tex set_bitmap {sprint "track/custom_track_textures/ss_strum_" $poorspystrumtype "_keep.png"}}
      {if_else $customsmashercolor
               {gs_tile0.mat set color {pack_color $smashercolor_r $smashercolor_g $smashercolor_b}}
               {gs_tile0.mat set color {pack_color 0.09411764706 0.6784313725 0.0}}
            }
      {if_else $customsmashercolor
               {gs_tile1.mat set color {pack_color $smashercolor_r $smashercolor_g $smashercolor_b}}
               {gs_tile1.mat set color {pack_color 0.631372549 0.01960784314 0.1176470588}}
            }
      {if_else $customsmashercolor
               {gs_tile2.mat set color {pack_color $smashercolor_r $smashercolor_g $smashercolor_b}}
               {gs_tile2.mat set color {pack_color 0.8823529412 0.8 0.12}}
            }
      {if_else $customsmashercolor
               {gs_tile3.mat set color {pack_color $smashercolor_r $smashercolor_g $smashercolor_b}}
               {gs_tile3.mat set color {pack_color 0.01960784314 0.1882352941 0.631372549}}
            }
      {if_else $customsmashercolor
               {gs_tile4.mat set color {pack_color $smashercolor_r $smashercolor_g $smashercolor_b}}
               {gs_tile4.mat set color {pack_color 0.6901960784 0.3960784314 0.0}}
            }
      {gs_tile5.mat set color {pack_color $poorspystrum_r $poorspystrum_g $poorspystrum_b}}
      {gs_tile6.mat set color {pack_color $poorspystrum_r $poorspystrum_g $poorspystrum_b}}
      {$this poorspy_lbl_update}
   )
)
{new
   GHPanel ;panel type
   poorspy_image_panel_buffer ;panel name
   POORSPY_PANEL_HANDLER
}
{new
   GHPanel ;panel type
   poorspy_image_panel ;panel name
   POORSPY_PANEL_HANDLER
}
{new MidiLoaderPanel midi_loader_panel} ;oh yeah look at all these options we have here...
{new GamePanel game
   (file "") ;ya got me, but yeah game panel doesnt load a milo
   (track_extend_sec -2) ;set the default timer for the track extend script
   (encore_unlock_check FALSE) ;encores are not available in dx 3.0
   (encore_song {if_else $forceencore TRUE FALSE}) ;encores are not available in dx 3.0
   (load {set_loader_period 13})
   (enter ;when the screen is entered
      {if_else $forceencore
         {do
            {set $encore TRUE}
            {game cheat_force_encore}
            {set {game want_encore_fx} TRUE}
         }
         {do
            {set $encore FALSE}
            {set {game want_encore_fx} FALSE}
         }
      }
      #ifdef HX_XBOX {rnd suspend} #endif ;no idea but ps2 does not have this code
      {set $p0_evenmtv TRUE}
      {set $p0_evenstreak TRUE}
      {set $p0_evenstar TRUE}
      {set $p0_evensec TRUE}
      {set $starrating 0} ;set current star back to default
      {set $starratingtemp 0} ;set temp star rating back to default
      {if_else {|| $autoplay $clonehero_hud}
         {set $starrating ' '} ;if autoplay, blank star rating display entirely
         {set $starrating ' ©'} ;otherwise, set displayed stars back to default
      }
      {streak_overlay_panel init_progress_bars} ;initialize the progress meters
      {set $notesmissed 0} ;set notes missed back to default
      {set $overstrums 0} ;set overstrums back to default
      {set $do_streak TRUE}
      {set $interval_p0 50} ;set streak interval back to default
      {set $interval_p1 50} ;set streak interval back to default
      {set $interval_p2 50} ;set streak interval back to default
      {set $player0_streak 0} ;set current streak back to default
      {set $player1_streak 0} ;set current streak back to default
      {set $player2_streak 0} ;set current streak back to default
      {set_loader_period 5}
      {if
         {&&
            {==
               {game get_venue}
               small1}
               {==
                  $encore
                  TRUE}}
         {{{world_panel loaded_dir}
               find
               drummer}
            show_drummer}}
   )
   (init_progress_handler
      {set $song_percent 0.0} ;reset the song progress variable to 0
      {streak_overlay_panel update_song_progress_task} ;update progress meter
      {set $song_length {game song_duration_sec}} ;get song length
;      {name_overlay_panel set_debug_line {sprintf "L:%0.3f S:%0.3f US:%0.3f PCT:%0.3f RPCT:%d" $song_length {taskmgr seconds} {taskmgr ui_seconds} $song_percent {player0 percent_complete}}} ;timer debug
      {script_task ;setup script task to run at a consistent interval
         (delay $progress_rate) ;rate set in ui/init.dta
         (units kTaskSeconds)
         (script
            {set $song_percent {* 100 {/ {taskmgr seconds} $song_length}}} ;calculate percent complete
;            {name_overlay_panel set_debug_line {sprintf "L:%0.3f S:%0.3f US:%0.3f PCT:%0.3f RPCT:%d" $song_length {taskmgr seconds} {taskmgr ui_seconds} $song_percent {player0 percent_complete}}} ;timer debug
            {streak_overlay_panel update_song_progress_task} ;update progress meter
            {streak_overlay_panel setup_next_progress_update} ;schedule next update
         )
      }
   )
   (intro_start_msg ;ran in quickplay/career by default on all songs
      ($fast $encore) ;setup local variables
      {track_panel intro_start_msg} ;call the intro_start_msg code block in track_panel
      {mtv_overlay_panel setup_text} ;hopefully initializing it here will fix the initial percentage state
      {mtv_overlay_panel_buffer setup_text} ;hopefully initializing it here will fix the initial percentage state
      {streak_overlay_panel init_progress_bars} ;initialize the progress meters
      {mtv_overlay_panel show_overlay FALSE} ;hide the mtv overlay for a few seconds
      {mtv_overlay_panel_buffer show_overlay FALSE} ;hide the mtv overlay for a few seconds
      {streak_overlay_panel show_overlay FALSE} ;hide the streak meter for a few seconds
      {switch {gamecfg get mode} ;dont keep the mtv overlay on screen for faceoff and pro faceoff
         (multi_vs {set $songtitle FALSE})
         (multi_fo {set $songtitle FALSE})
      }
      {if_else {! $fast} ;check if we are set to enter the world fast or not
         {do
            {if $do_streak
               {streak_overlay_panel setup_streak} ;if streak is supposed to run, setup the streak meter
            }
            {script_task
               (delay 1)
               (units kTaskSeconds) ;wait one second if fast is enabled
               (script
                  {mtv_overlay_panel show_overlay TRUE} ;always show the mtv overlay during the intro
               )
            }
            {script_task
               (delay 6)
               (units kTaskSeconds) ;wait six seconds if fast is disabled
               (script
                  {mtv_overlay_panel show_overlay ;hide the overlay if song title modifier is enabled, always show the overlay if autoplay is enabled
                     {if_else $autoplay
                        TRUE
                        $songtitle
                     }
                  }
               )
            }
         }
         {do
            {if $do_streak
               {streak_overlay_panel setup_streak} ;if streak is supposed to run, setup the streak meter
            }
            {script_task
               (delay 1)
               (units kTaskSeconds) ;wait one second if fast is enabled
               (script
                  {mtv_overlay_panel show_overlay TRUE} ;always show the mtv overlay during the intro
               )
            }
            {script_task
               (delay 3)
               (units kTaskSeconds) ;wait three seconds if fast is disabled
               (script
                  {mtv_overlay_panel show_overlay ;hide the overlay if song title modifier is enabled, always show the overlay if autoplay is enabled
                     {if_else $cheat_enabled_performance_mode
                        FALSE
                        {if_else $autoplay
                           TRUE
                           $songtitle
                        }
                     }
                  }
               )
            }
         }
      }
      {set [intro_complete] FALSE}
      kDataUnhandled
   )
   (intro_skip ;ran in practice mode or when intro skip/focus mode modifier is enabled. skips the intro camera shot and track extension
      {set $p0_evenmtv FALSE}
      {set $p0_evenstreak FALSE}
      {set $p0_evenstar FALSE}
      {set $p0_evensec FALSE}
      {set $starrating 0} ;set current star back to default
      {set $starratingtemp 0} ;set temp star rating back to default
      {if_else $autoplay
         {set $starrating ' '} ;if autoplay, blank star rating display entirely
         {set $starrating ' ©'} ;otherwise, set displayed stars back to default
      }
      {set $notesmissed 0} ;set notes missed back to default
      {set $overstrums 0} ;set overstrums back to default
      {track_panel intro_skip}
      {set $do_streak TRUE}
      {streak_overlay_panel streak_lastframe} ;should force streak overlay to appear in practice even after a run
      {mtv_overlay_panel setup_text} ;hopefully initializing it here will fix the initial percentage state
      {mtv_overlay_panel_buffer setup_text} ;hopefully initializing it here will fix the initial percentage state
      {streak_overlay_panel show_overlay TRUE} ;force show streak meter
      {mtv_overlay_panel show_overlay TRUE} ;force show mtv overlay
      {if {"||" $cheat_enabled_performance_mode {! $autoplay} {! $songtitle}} ;if not autoplay or if song title modifier is turned off
         {script_task
            (delay 3)
            (units kTaskSeconds) ;wait 3 seconds
            (script
               {mtv_overlay_panel show_overlay FALSE}
               {mtv_overlay_panel_buffer show_overlay FALSE}
            )
         } ;and hide the mtv overlay
      }
      {if_else $towel ;check if towel hyperspeed modifier is enabled
         {beatmatch set_music_speed 1.0} ;if enabled force the music speed to be 100%
         {if_else {> $speedmod 1} ;if towel is disabled, check if song speed is greater than 100%
            {beatmatch set_music_speed $speedmod} ;if music speed is set to greater than 100%, set the beatmatcher speed to that value
            {beatmatch set_music_speed 1.0} ;if music speed is set to 100%, set beatmatcher speed to 100%
         }
      }
      {$this slide_meter_in} ;animate all of the hud meters in to position
      {if $do_streak
         {streak_overlay_panel setup_streak} ;if streak is supposed to run, setup the streak meter
      }
      {streak_overlay_panel show_overlay $do_streak} ;show the streak meter if it is allowed to run
      {$this init_progress_handler} ;initialize progress bar handler
      kDataUnhandled
   )
   (extend_track ;ran after the intro camera shot, handles similar items to intro_skip, but this is ran when intro_skip is disabled
      {world_panel set_showing {! $focusmode}} ;show the world panel or not if focus mode
      {if $focusmode
         {world do_force_shot lighter} ;if focus mode, force a specific camera shot available in all venues
      }
      {if {&& $focusmode {|| {== {gamecfg get mode} career} {== {gamecfg get mode} quickplay} {== {gamecfg get mode} multi_coop}}} ;if focus mode and not practice mode
         {if {exists world}
            {world toggle_pick_shot} ;check if the world exists, and prevent the camera from leaving the lighter shot
         }
      }
      {if {&& {! $neckless} {! $cheat_enabled_performance_mode}} ;if not neckless mode or performance mode
         {track_panel do_extend_sequence} ;extend the track out
      }
      {if_else $towel ;check if towel hyperspeed modifier is enabled
         {beatmatch set_music_speed 1.0} ;if enabled force the music speed to be 100%
         {if_else {> $speedmod 1} ;if towel is disabled, check if song speed is greater than 100%
            {beatmatch set_music_speed $speedmod} ;if music speed is set to greater than 100%, set the beatmatcher speed to that value
            {beatmatch set_music_speed 1.0} ;if music speed is set to 100%, set beatmatcher speed to 100%
         }
      }
      {script_task
         (delay 1.8)
         (units kTaskSeconds) ;wait 1.8 seconds
         (script
            {handle
               ($this slide_meter_in) ;animate the hud meters in
            }
         )
      }
      {script_task
         (delay 2)
         (units kTaskSeconds) ;wait 2 seconds
         (script
            {handle
               ($this intro_end)
               (streak_overlay_panel show_overlay $do_streak) ;run intro_end code and show the streak meter if it is running
            }
         )
      }
      {set [intro_complete] TRUE} ;this will allow the pause>restart button to restart back to the track extend, instead of the full intro shot every restart
      {$this init_progress_handler} ;initialize progress bar handler
   )
   (music_start
      {ui music_start} ;start the music
      {if {|| $cheat_enabled_performance_mode {! $songtitle}}
         {mtv_overlay_panel set_showing FALSE}
         {mtv_overlay_panel_buffer set_showing FALSE}
         {name_overlay_panel set_showing FALSE}
      }
      kDataUnhandled
   )
   (beat ;runs code every beat of the currently playing song
;      {streak_overlay_panel update_song_progress} ;moving this to a timer
      {if $barf_cheat ;check for roxbury modifier
         {track_panel animate (range 2000 2025) (period 0.4)} ;if roxbury enabled, shake the track on beat of the song
      }
      {if $debug.beep
         {play_sfx click.cue} ;debug click track
      }
      {if {! {game is_tutorial_running}} ;check if the tutorial is running
         {do
            ($clap FALSE) ;setup clap variable
            {$this foreach_player $p ;run the following code for each player currently enabled
               {if {$p in_star_mode} ;check if the current player has star power activated
                  {if_else $focusmode ;check if focus mode is enabled
                     {set $clap FALSE} ;if focus mode is enabled, do not clap on beat in star power
                     {set $clap TRUE} ;if focus mode is disabled, clap to the beat during star power
                  }
               }
            }
            {if $clap ;if clap turns to true after the above is ran
               {script_task
                  (units kTaskBeats)
                  (delay CLAP_DELAY_BEATS) ;run clap on beat
                  (script {handle (world play_sfx CLAP_SEQUENCE)}) ;play the audio for the clap
               }
            }
          }
      }
      kDataUnhandled
   )
   (downbeat ;runs code every down beat of the currently playing song
      {$this foreach_player $p
         {$p on_downbeat} ;run the downbeat code for each player currently enabled
      }
      kDataUnhandled
   )
   (game_over ;ran when song failed
      {$this foreach_player $p
         {$p on_game_over}
      }
      {stop_sfx sp_deployed} ;stop playing sp noisess
      {stop_sfx sp_deployed_multi} ;stop playing sp noisess
      {stop_sfx sp_deploystart.cue} ;stop playing sp noisess
      {streak_overlay_panel show_overlay FALSE} ;hide the streak overlay
      {name_overlay_panel set_showing FALSE}
      {mtv_overlay_panel set_showing FALSE}
      {mtv_overlay_panel_buffer set_showing FALSE}
      {poorspy_image_panel set_showing FALSE}
      {poorspy_image_panel_buffer set_showing FALSE}
      {if_else {== practice {gamecfg get mode}}
         kDataUnhandled
         POORSPY_RESET_HANDLER
      }
      kDataUnhandled
   )
   (game_won_msg ;ran on song completion
   {if_else $forceencore
         {do
            {set $encore TRUE}
            {game cheat_force_encore}
            {set {game want_encore_fx} TRUE}
         }
         {do
            {set $encore FALSE}
            {set {game want_encore_fx} FALSE}
         }
      }
   ($encore)
      {poorspy_image_panel set_showing FALSE}
      {poorspy_image_panel_buffer set_showing FALSE}
      {set $author FALSE} ;we are done with the author flag for this song, reset to false
      {name_overlay_panel set_showing FALSE}
      {mtv_overlay_panel set_showing FALSE}
      {mtv_overlay_panel_buffer set_showing FALSE}
      {beatmatch set_music_speed 1.0} ;reset music speed to 1 so that the "outro" takes the right amount of time
      {if_else $skipoutro
         {do
            {hud set_showing FALSE}
            {track_panel set_showing FALSE}
            {script_task
               (delay 0.5)
               (units kTaskSeconds)
               (script
                  {handle (game set_outro_complete TRUE)}
               )
            }
         kDataUnhandled ;do nothing if outro skipping is enabled
         }
         {do
            {if {&& $focusmode {'||' {== {gamecfg get mode} career} {== {gamecfg get mode} quickplay} {== {gamecfg get mode} multi_coop}}} ;if focus mode and game mode is career or quickplay or coop
               {if {exists world}
                  {world toggle_pick_shot} ;check if the world exists, and allow camera cuts again
               }
            }
            {world_panel set_showing TRUE} ;always show the world when a song is complete
            {if_else {== {gamecfg get mode} practice} ;check if the current game mode is practice
               kDataUnhandled ;if practice mode, do nothing
                  {do
                     {if
               {&&
                  {==
                     {game get_venue}
                     small1}
                     $encore
               }
               {do
                  {script_task
                     (delay DRUMMER_EXPLODE_DELAY)
                     (units kTaskSeconds)
                     (script
                        {handle
                           ($this drummer_explode)}
                        {play_sfx drummer_exp}
                        {handle
                           ({{world_panel loaded_dir}
                                 find
                                 guitarist0}
                              game_won_msg
                              $encore)})}}}
                     #ifdef HX_XBOX ;xbox acheivement handling
                     {if {! {gamecfg multiplayer}} ;if singleplayer
                        {achievements submit_points {player0 score}} ;submit singleplayer score to leaderboards
                     }
                     {if {== {player0 percent_hit} 100}
                        {achievements submit perfect}
                     }
                     {if {options get_lefty 0} ;if lefty flip enabled for player 1
                        {achievements submit lefty} ;pop up achievement
                     }
                     {if {&& {== {gamecfg get_difficulty 0} kDifficultyExpert} {== {gamecfg get_song} jordan}} ;if difficulty expert and the song shortname is "jordan"
                        {achievements submit jordan} ;pop up achievement
                     }
                     {if {== multi_coop {gamecfg get mode}} ;check if the current gamemode is coop
                        {if {== {band percent_hit} 100} ;if all players hit 100% of the notes
                           {achievements submit_coop perfect} ;pop up achievement
                        }
                        {achievements submit_coop_points {band score}} ;submit coop score to leaderboards
                     }
                     #endif ;done with xbox specific code
                     {track_panel explode} ;animate the track exploding
                     {play_game_sfx TRACK_EXPLODE_SFX} ;track explode sound effect
                     {game set_realtime TRUE} ;return to default game speed
                     {beatmatch set_paused TRUE} ;pause the beatmatcher since it's exploded
                     {if $encore {encore_stream play}}
                     kDataUnhandled
                  }
            }
         }
      }
   )
   (game_lost ;run when the song is failed
      {set $author FALSE} ;set the author to false to be set again if restarted
      {if {&& $focusmode {'||' {== {gamecfg get mode} career} {== {gamecfg get mode} quickplay} {== {gamecfg get mode} multi_coop}}} ;if focus mode and game mode is career or quickplay or coop
         {if {exists world}
            {world toggle_pick_shot} ;check if the world exists, and allow camera cuts again
         }
      }
      {world_panel set_showing TRUE} ;set the world showing to true no matter what
      {track_panel set_showing FALSE} ;hide the track panel because you failed and you suck
      {name_overlay_panel set_showing FALSE}
      {mtv_overlay_panel set_showing FALSE}
      {mtv_overlay_panel_buffer set_showing FALSE}
      {poorspy_image_panel set_showing FALSE}
      {poorspy_image_panel_buffer set_showing FALSE}
      {game set_realtime TRUE} ;return to default game speed
      {$this slow_music} ;slow down the music based on LOSE_MUSIC_SLOW_SEC define
      #ifdef HX_XBOX ;xbox acheivement handling
      {if {&& {== {gamecfg get_difficulty 0} kDifficultyEasy} {! {gamecfg multiplayer}}} ;if the current difficulty is easy, and the mode is singleplayer
         {achievements submit faileasy} ;pop up achievement
      }
      #endif ;done with xbox specific code
      kDataUnhandled
   )
   (game_outro_complete ;run when the song outro is completed via fail or song complete
      {synth stop_all_sfx} ;stop all sound effects
      {game set_paused TRUE} ;freeze the game screen
      {if_else [won] ;check if the song was beaten or if you failed
         {do ;if beaten
            {ui goto_screen post_show_screen}
         }
         {do ;if failed
            {world_panel set_paused TRUE} ;freeze the world
            {ui goto_screen {gamecfg get lose_screen}} ;go to the current mode designated lose screen noted in modes.dta
         }
      }
      kDataUnhandled
   )
   (blew_big_note ;ran when you miss a specific "big note" defined in the midi
      ($player) ;setup player variable
      {if {crowd_audio enabled}
         {world play_sfx blew_bignote.cue} ;if crowd audio is enabled, play the sound effect
      }
   )
   #define PRACTICE_END_FADE_SEC (1) ;how many seconds until the practice end screen shows on section completion
   (practice_end ;ran at the end of selected sections in practice mode
      {music_fader fade -50 {'*' PRACTICE_END_FADE_SEC 1000}} ;fade out the audio when section is completed
      {script_task
         (delay PRACTICE_END_FADE_SEC)
         (units kTaskSeconds) ;wait until the music is faded out
         (script
            {$this set_paused TRUE}
            {ui goto_screen practice_end_screen} ;freeze the game and pop up the practice end screen
         )
      }
   )
   #define LOSE_MUSIC_SLOW_SEC (2) ;how many seconds the music slows down when you fail
   (slow_music ;ran when the player fails a song
      {set [lose_time] {taskmgr seconds}} ;start a timer for the slowdown
      {script_task (once FALSE) ;run this script repeatedly until the timer runs out
         (script
            {do
               ($t {- {taskmgr seconds} [lose_time]}) ;setup time variable
               {if_else {< $t LOSE_MUSIC_SLOW_SEC} ;check if the timer has reached the defined period
                  {do
                     {beatmatch set_music_speed {/ {- LOSE_MUSIC_SLOW_SEC $t} LOSE_MUSIC_SLOW_SEC}} TRUE} ;slow down the music based off the current timer
                  {do
                     {beatmatch set_paused TRUE} FALSE} ;if the timer has ended, pause the music completely
               }
            }
         )
      }
   )
   (end_streak ;ran when a note streak has been broken
      ($player $playerobj) ;setup variables
      {if {gamecfg get blown_streak_feedback} ;if the current mode allows blown streak feedback
         {do
            ($mult) ;setup variable
            {if_else {== multi_coop {gamecfg get mode}} ;check if the current game mode is coop
               {set $mult {band base_multiplier}} ;if coop, check the whole band multiplier
               {set $mult {$playerobj base_multiplier}} ;if singleplayer, check only the first player
            }
            {if {'&&' $highwayshake {'==' $trackcamx 1.0} {> $mult 1}} ;if highwayshake is enabled and the multiplier is above 1x
               {handle (game blow_streak $player $mult)} ;shake the highway for the player in severity based off the current multiplier
            }
         }
      }
   )
   (blow_streak ;ran when a player misses in a streak
      ($player $mult) ;setup the variables
      {do
         ($sfx {sprintf "streak_broken_%ix.cue" $mult}) ;setup which sound to use via variables
         {if $sfx
            {play_game_sfx_player $sfx $sfx $player}
         } ;only play miss streak sound effects if focus mode is not enabled
      }
      kDataUnhandled
   )
   (force_encore FALSE) ;encores are not available in dx 3.0
   (lose_time 0) ;sets lose timer default
   (intro_complete FALSE) ;game defaults intro complete to false and script sets up later
}
{new UIPanel world_panel
   (file
      {sprint "../world/" {game get_venue} "/" {game get_venue} ;handle choosing the currently selected venue and parsing it's location on ark
         {if_else {game multiplayer} ;check if the game is coop
            {if_else {game multiplayer_vs} ;check if the game is faceoff
               "_mp.milo" ;load faceoff venue milos
               "_coop.milo" ;load coop venue milos
            }
            ".milo" ;load standard venue milos
         }
      }
   )
   (finish_load
      {{$this loaded_dir} set_name world main} ;set the world name to "main"
      #ifdef HX_XBOX {world set echo_msgs $echo_world_events} #endif ;handle world events if on xbox
   )
   (enter ;when the screen is entered
      {if $songrandomized ;check if we entered the game from a random song
         {set $songrandomized FALSE} ;set song randomizer back to false to return to the correct position when finished with the song
      }
      {if $focusmode
         {crowd_audio set_enabled FALSE}} ;if focus mode is enabled, mute the crowd audio
      {if_else {game multiplayer} ;check if the game is coop/pro/faceoff
         {handle ($this multi_player)} ;multi player code handle
         {handle ($this single_player)} ;single player code handle
      }
      {$this set_showing {! $focusmode}} ;hide the world if focus mode is disabled?
      {if {< $emulatefps 60}
         {$this iterate PostProc $p
            {$p set emulate_fps $emulatefps} ;set the venue fps to the defined fps
         }
      }
      {$this set_paused FALSE} ;unfreeze the world screen
   )
#ifdef HX_XBOX (unload {rnd suspend}) #endif ;no idea but ps2 does not have this code
}
#define FADE_LENGTH_SEC (2)
{new FadePanel fade_panel
   (file fade.milo)
   (enter ;when the screen is entered
      {do
         {press_any_button.lbl set_showing FALSE} ;attract is not available in dx 3.0 so always hide the label and mesh
         #ifdef HX_XBOX {logo.mesh set_showing FALSE} #endif ;attract is not available in dx 3.0 so always hide the label and mesh
      }
   )
}
{new GHScreen finish_fade
   (panels fade_panel) ;panels used in this screen, in draw order
   (enter ;when the screen is entered
      {meta_loading_goto main_screen} ;go to main screen, previously splash screen
   )
}
#define SLOT_TASK_HANDLERS
(
   (slot_duration
      {if $slotstate_0
         {set $slot_duration_0 {+ $slot_duration_0 $scorespyupdatesec}}
      }
      {if $slotstate_1
         {set $slot_duration_1 {+ $slot_duration_1 $scorespyupdatesec}}
      }
      {if $slotstate_2
         {set $slot_duration_2 {+ $slot_duration_2 $scorespyupdatesec}}
      }
      {if $slotstate_3
         {set $slot_duration_3 {+ $slot_duration_3 $scorespyupdatesec}}
      }
      {if $slotstate_4
         {set $slot_duration_4 {+ $slot_duration_4 $scorespyupdatesec}}
      }
      {if $slotstate_5
         {set $slot_duration_5 {+ $slot_duration_5 $scorespyupdatesec}}
      }
      {if $slotstate_6
         {set $slot_duration_6 {+ $slot_duration_6 $scorespyupdatesec}}
      }
      {if {! $autoplay}
         {if_else $p0_evenpoorspy
            {switch $player ;streak is even, write to line 2 while 1 is showing then swap
               (0
                  {poorspy_image_panel_buffer poorspy_lbl_update}
                  {poorspy_image_panel set_showing FALSE}
                  {poorspy_image_panel_buffer set_showing TRUE}
                  {set $p0_evenpoorspy FALSE}
               )
               kDataUnhandled
            }
            {switch $player ;streak is odd, write to line 1 while 2 is showing then swap
               (0 
                  {poorspy_image_panel poorspy_lbl_update}
                  {poorspy_image_panel_buffer set_showing FALSE}
                  {poorspy_image_panel set_showing TRUE}
                  {set $p0_evenpoorspy TRUE}
               )
               kDataUnhandled
            }
         }
      }
   )
)
#define SLOT_BUTTON_DOWN_HANDLE
   (
      {if {== $button kPad_X} ;if is pressed
         {set $slotstate_0 TRUE}
         {set $slotstate_count_0 {+ $slotstate_count_0 1}}
         {if_else {exists slot_duration_task} ;check if the below task is running
            kDataUnhandled
            {if {== $slotstate_count_0 1}
               {thread_task ;run a new task
                  (units kTaskSeconds) ;we are in ui so use ui seconds
                  (name slot_duration_task) ;name the task for pruning later
                  (script
                     {$this slot_duration} ;run update_char_pic block
                     {$task sleep $scorespyupdatesec} ;wait
                     {$task loop} ;loop task
                  )
               }
            }
         }
      }
      {if {== $button kPad_Tri} ;if is pressed
         {set $slotstate_1 TRUE}
         {set $slotstate_count_1 {+ $slotstate_count_1 1}}
         {if_else {exists slot_duration_task} ;check if the below task is running
            kDataUnhandled
            {if {== $slotstate_count_1 1}
               {thread_task ;run a new task
                  (units kTaskSeconds) ;we are in ui so use ui seconds
                  (name slot_duration_task) ;name the task for pruning later
                  (script
                     {$this slot_duration} ;run update_char_pic block
                     {$task sleep $scorespyupdatesec} ;wait
                     {$task loop} ;loop task
                  )
               }
            }
         }
      }
      {if {== $button kPad_Circle} ;if is pressed
         {set $slotstate_3 TRUE}
         {set $slotstate_count_3 {+ $slotstate_count_3 1}}
         {if_else {exists slot_duration_task} ;check if the below task is running
            kDataUnhandled
            {if {== $slotstate_count_3 1}
               {thread_task ;run a new task
                  (units kTaskSeconds) ;we are in ui so use ui seconds
                  (name slot_duration_task) ;name the task for pruning later
                  (script
                     {$this slot_duration} ;run update_char_pic block
                     {$task sleep $scorespyupdatesec} ;wait
                     {$task loop} ;loop task
                  )
               }
            }
         }
      }
      {if {== $button kPad_Square} ;if is pressed
         {set $slotstate_2 TRUE}
         {set $slotstate_count_2 {+ $slotstate_count_2 1}}
         {if_else {exists slot_duration_task} ;check if the below task is running
            kDataUnhandled
            {if {== $slotstate_count_2 1}
               {thread_task ;run a new task
                  (units kTaskSeconds) ;we are in ui so use ui seconds
                  (name slot_duration_task) ;name the task for pruning later
                  (script
                     {$this slot_duration} ;run update_char_pic block
                     {$task sleep $scorespyupdatesec} ;wait
                     {$task loop} ;loop task
                  )
               }
            }
         }
      }
      {if {== $button kPad_L1} ;if is pressed
         {set $slotstate_4 TRUE}
         {set $slotstate_count_4 {+ $slotstate_count_4 1}}
         {if_else {exists slot_duration_task} ;check if the below task is running
            kDataUnhandled
            {if {== $slotstate_count_4 1}
               {thread_task ;run a new task
                  (units kTaskSeconds) ;we are in ui so use ui seconds
                  (name slot_duration_task) ;name the task for pruning later
                  (script
                     {$this slot_duration} ;run update_char_pic block
                     {$task sleep $scorespyupdatesec} ;wait
                     {$task loop} ;loop task
                  )
               }
            }
         }
      }
      {if {== $button kPad_DUp} ;if is pressed
         {if {! $slotstate_5} {set $slotstate_count_5 {+ $slotstate_count_5 1}}}
         {set $slotstate_5 TRUE}
         {if_else {exists slot_duration_task} ;check if the below task is running
            kDataUnhandled
            {if {== $slotstate_count_5 1}
               {thread_task ;run a new task
                  (units kTaskSeconds) ;we are in ui so use ui seconds
                  (name slot_duration_task) ;name the task for pruning later
                  (script
                     {$this slot_duration} ;run update_char_pic block
                     {$task sleep $scorespyupdatesec} ;wait
                     {$task loop} ;loop task
                  )
               }
            }
         }
      }
      {if {== $button kPad_DDown} ;if is pressed
         {if {! $slotstate_6} {set $slotstate_count_6 {+ $slotstate_count_6 1}}}
         {set $slotstate_6 TRUE}
         {if_else {exists slot_duration_task} ;check if the below task is running
            kDataUnhandled
            {if {== $slotstate_count_6 1}
               {thread_task ;run a new task
                  (units kTaskSeconds) ;we are in ui so use ui seconds
                  (name slot_duration_task) ;name the task for pruning later
                  (script
                     {$this slot_duration} ;run update_char_pic block
                     {$task sleep $scorespyupdatesec} ;wait
                     {$task loop} ;loop task
                  )
               }
            }
         }
      }
   )
#define SLOT_BUTTON_UP_HANDLE
(
   (BUTTON_UP_MSG
      {if {== $button kPad_X} ;if is pressed
         {set $slotstate_0 FALSE}
      }
      {if {== $button kPad_Tri} ;if is pressed
         {set $slotstate_1 FALSE}
      }
      {if {== $button kPad_Circle} ;if is pressed
         {set $slotstate_2 FALSE}
      }
      {if {== $button kPad_Square} ;if is pressed
         {set $slotstate_3 FALSE}
      }
      {if {== $button kPad_L1} ;if is pressed
         {set $slotstate_4 FALSE}
      }
      {if {== $button kPad_DUp} ;if is pressed
         {set $slotstate_5 FALSE}
      }
      {if {== $button kPad_DDown} ;if is pressed
         {set $slotstate_6 FALSE}
      }
   )
)
#define GAME_PANELS
(midi_loader_panel game track_mask world_panel track_panel hud name_overlay_panel poorspy_image_panel poorspy_image_panel_buffer mtv_overlay_panel_buffer mtv_overlay_panel streak_overlay_panel) ;define the panels used in the game screen, in draw order
{new GHScreen game_screen
   (panels GAME_PANELS
   #ifdef HX_XBOX ;inside isnt fixed for ps2 yet
   fade_panel
   #endif) ;panels used in this screen, in draw order
   (allow_back FALSE) ;set no default back screen when this screen is loaded
   (animate_transition FALSE) ;do not animate to this screen, cut to it
   (in_game TRUE) ;defines this screen as one that runs during game logic
   (attract_mode FALSE) ;attract mode is not used in dx 3.0
   SLOT_TASK_HANDLERS ;poorspy task handlers
   (BUTTON_DOWN_MSG ;code to run when a specific button is pressed
      {if {== $button kPad_Start} ;if start is pressed
         {game set_paused TRUE} ;freeze the game
         {ui goto_screen pause_screen} ;open the pause screen
      }
      SLOT_BUTTON_DOWN_HANDLE ;poorspy button down macros
   )
   SLOT_BUTTON_UP_HANDLE ;poorspy button up macros
   (TRANSITION_COMPLETE_MSG ;run when the pause screen is closed
      {if $game_unpause_on_enter ;check if we are entering the game from the pause screen
         {game set_paused FALSE} ;unfreeze the game
         {world_panel set_paused FALSE} ;unfreeze the world
         {set $game_unpause_on_enter FALSE} ;reset the unpause location state
      }
   )
#ifdef HX_XBOX
   (enter
      {poorspy_image_panel set_showing {! $autoplay}}
      {poorspy_image_panel_buffer set_showing FALSE}
      {track_mask set_showing TRUE} ;show the track mask, only runs on xbox
   )
#endif
}
{new GHScreen reload_track_screen
   (panels game world_panel hud mtv_overlay_panel name_overlay_panel fade_panel) ;panels used in this screen, in draw order
   (allow_back FALSE) ;set no default back screen when this screen is loaded
   (animate_transition FALSE) ;do not animate to this screen, cut to it
   (enter ;when the screen is entered
      {game set_paused TRUE} ;freeze the game
      {game_restart_fast} ;quick restart the current song
   )
}