#include game_track_panel.dta
#include game_hud_panel.dta
{new GHPanel mtv_overlay_panel
   (file mtv_overlay.milo)
   (enter ;when the screen is entered
      {mtv_overlay.view set_local_scale .5 1 .5} ;make the mtv overlay small
	  {mtv_overlay.view set_local_pos -240 1 85} ;move the mtv overlay to the top left corner of the screen
      {$this setup_text} ;run the setup_text code block below
   )
   (setup_text ;called when mtv screen is entered, as well as several other places in the hud panel to refresh stats defined below
      {do
         ($gems_hit {player0 gems_hit}) ;setup gems hit variable
         ($gems_total {'+' $gems_hit {player0 gems_passed}}) ;setup total gems that have passed the strikeline variable
         ($song {gamecfg get_song}) ;setup song variable
         ($song_text {game get_song_text}) ;setup song text variable
         ($artist_text {game get_song_artist_text}) ;setup artist variable
         {do
            ($prefix "mtv_campaign_line") ;setup prefix variable to switch between line numbers
            {mtv_campaign_song_id.view set_showing TRUE} ;show the mtv overlay
            {if_else {!= $speedmod 1} ;if song speed is set to anything other than 100%
               {if_else {&& {== {gamecfg get mode} practice} {!= {gamecfg get practice_speed} 0}} ;now check if the game mode is practice and the practice speed is set to full speed
                  {$this set_line $prefix 1 $song_text} ;if all speeds are 100% simply show the song name on line 1
                  {$this set_line $prefix 1 {sprint $song_text " (" {int {'+' 0.5 {'*' $speedmod 100}}} "%)"}} ;if song speed is not 100%, do math to report the song speed as a percentage on line 1
			   }
               {$this set_line $prefix 1 $song_text} ;if all speeds are 100% simply show the song name on line 1
			}
            {$this set_line $prefix 2 {sprint "BY - " $artist_text}} ;the artist text line is always just "BY - ARTIST"
			{if_else {!= "" {localize {sprint {gamecfg get_song} "_author"}}} ; If not debug, check for author string in locale
               {if_else $autoplay ;check for autoplay enabled
			      {$this set_line $prefix 3 {sprint "\nAUTOPLAY"}} ;if autoplay is enabled, report nothing below, only "AUTOPLAY" on line 3
			      {$this set_line $prefix 3 ;if it is not autoplay, and author is found in locale. all on line 3
				     {sprint "\nAuthor: " {localize {sprint $song "_author"}} "\n" ;apply the author locale and new line
					 $notesmissed " MISSED - " $overstrums " OVERSTRUMS\n" ;apply the notes missed and overstum variables we set in streak panel sinks, and new line
				     {sprint {* {/ {if_else $beforefirstnote_p0 {+ $gems_hit 1} $gems_hit} {if_else $beforefirstnote_p0 {+ $gems_total 1} $gems_total}} 100} "%"}} ;do some math to report the current notes hit out of notes passed as a percentage
				  }
			   }
			         {if_else $autoplay ;if an author locale string is not found, check for autoplay enabled
			            {$this set_line $prefix 3 {sprint "\nAUTOPLAY"}} ;if autoplay is enabled, report nothing below, only "AUTOPLAY" on line 3
			               {if_else $author ; check for a set author variable in DLC contexts.dta
                              {$this set_line $prefix 3 ;if it is not autoplay, and author variable is set. all on line 3
							     {sprint "\nAuthor: " $author "\n" ;apply the author variable and new line
								 $notesmissed " MISSED - " $overstrums " OVERSTRUMS\n" ;apply the notes missed and overstum variables we set in streak panel sinks, and new line
								 {sprint {* {/ {if_else $beforefirstnote_p0 {+ $gems_hit 1} $gems_hit} {if_else $beforefirstnote_p0 {+ $gems_total 1} $gems_total}} 100} "%"}}  ;do some math to report the current notes hit out of notes passed as a percentage
							  }
                                    {$this set_line $prefix 3 ;if it is not autoplay and no author variable is set. all on line 3
								       {sprint "\n" $notesmissed " MISSED - " $overstrums " OVERSTRUMS\n" ;apply the notes missed and overstum variables we set in streak panel sinks, and new line
									   {sprint {* {/ {if_else $beforefirstnote_p0 1 $gems_hit} {if_else $beforefirstnote_p0 1 $gems_total}} 100} "%"}}  ;do some math to report the current notes hit out of notes passed as a percentage
			                        }
			               }
			         }
			}
         }
	  }
   )
   (set_line ;called in mtv overlay to apply the text to two labels at once
      ($prefix $i $text) ;setup variables
      {{sprint $prefix $i ".lbl"} set_localized_text $text} ;set the main text objects
      {{sprint $prefix $i "_shadow.lbl"} set_localized_text $text} ;set the shadow text objects
   )
   (show_overlay ;code to run to show the overlay or not based on a variable
      ($show) ;setup variable
      {$this set_showing $show} ;show or hide based off variable
      {$this set_paused FALSE} ;we refresh the mtv overlay alot, so we never want it paused
	  )}
{new MidiLoaderPanel midi_loader_panel} ;oh yeah look at all these options we have here...
{new GamePanel game
   (file "") ;ya got me, but yeah game panel doesnt load a milo
   (track_extend_sec -2) ;set the default timer for the track extend script
   (encore_unlock_check FALSE) ;encores are not available in dx 3.0
   (encore_song FALSE) ;encores are not available in dx 3.0
   (load {set_loader_period 13})
   (enter ;when the screen is entered
      #ifdef HX_XBOX {rnd suspend} #endif ;no idea but ps2 does not have this code
	  {set $starrating 0} ;set current star back to default
	  {set $starratingtemp 0} ;set temp star rating back to default
      {set $starrating '0 ©'} ;set set displayed stars back to default
      {set $notesmissed 0} ;set notes missed back to default
      {set $overstrums 0} ;set overstrums back to default
	  {set $do_streak TRUE} ;set the streak meter code to enabled
      {set $interval_p0 50} ;set streak interval back to default
      {set $interval_p1 50} ;set streak interval back to default
      {set $interval_p2 50} ;set streak interval back to default
      {set $player0_streak 0} ;set current streak back to default
      {set $player1_streak 0} ;set current streak back to default
      {set $player2_streak 0} ;set current streak back to default
      {set_loader_period 5}
      ;{set [encore_unlock_check] {&& {== {gamecfg get mode} career} {campaign encore_unlock_potential}}} ;encores are not available in dx 3.0
      ;{print "encore unlock check is " [encore_unlock_check] "\n"} ;debug printing for current encore
      ;{set [encore_song] {&& {!= {gamecfg get mode} tutorial} {campaign is_encore_song {gamecfg get_song}}}} ;encores are not available in dx 3.0
      ;{print "encore song is " [encore_song] "\n"} ;debug printing for current encore
   )
   (intro_start_msg ;ran in quickplay/career by default on all songs
      ($fast $encore) ;setup local variables
      {track_panel intro_start_msg} ;call the intro_start_msg code block in track_panel
      {mtv_overlay_panel show_overlay FALSE} ;hide the mtv overlay for a few seconds
      {streak_overlay_panel show_overlay FALSE} ;hide the streak meter for a few seconds
      {switch {gamecfg get mode} (multi_vs {set $songtitle FALSE}) (multi_fo {set $songtitle FALSE})} ;dont keep the mtv overlay on screen for faceoff and pro faceoff
      ;{if $encore {do {world_panel set_showing TRUE} {if $focusmode {set $encorefocus TRUE}}}} ;encores are not available in dx 3.0
      {if_else {! $fast} ;check if we are set to enter the world fast or not
         {do
            {if $do_streak {streak_overlay_panel setup_streak}} ;if streak is supposed to run, setup the streak meter
            {script_task (delay 1) (units kTaskSeconds) ;wait one second if fast is enabled
               (script {mtv_overlay_panel show_overlay TRUE}) ;always show the mtv overlay during the intro
			}
            {script_task (delay 6) (units kTaskSeconds) ;wait six seconds if fast is disabled
               (script {mtv_overlay_panel show_overlay {if_else $autoplay TRUE $songtitle}}) ;hide the overlay if song title modifier is enabled, always show the overlay if autoplay is enabled
			}
         }
         {do
            {if $do_streak {streak_overlay_panel setup_streak}} ;if streak is supposed to run, setup the streak meter
            {script_task (delay 1) (units kTaskSeconds) ;wait one second if fast is enabled
               (script {mtv_overlay_panel show_overlay TRUE}) ;always show the mtv overlay during the intro
			}
            {script_task (delay 3) (units kTaskSeconds) ;wait three seconds if fast is disabled
               (script {mtv_overlay_panel show_overlay {if_else $autoplay TRUE $songtitle}}) ;hide the overlay if song title modifier is enabled, always show the overlay if autoplay is enabled
			}
	     }
	  }
      {set [intro_complete] FALSE}
      kDataUnhandled)
   (intro_skip ;ran in practice mode or when intro skip/focus mode modifier is enabled. skips the intro camera shot and track extension
      {set $starrating 0} ;set current star back to default
	  {set $starratingtemp 0} ;set temp star rating back to default
      {set $starrating '0 ©'} ;set set displayed stars back to default
      {set $notesmissed 0} ;set notes missed back to default
      {set $overstrums 0} ;set overstrums back to default
      {track_panel intro_skip}
	  {set $do_streak TRUE} ;set the streak meter code to enabled
      {streak_overlay_panel streak_lastframe} ;should force streak overlay to appear in practice even after a run
      {streak_overlay_panel show_overlay TRUE} ;force show streak meter
      {mtv_overlay_panel show_overlay TRUE} ;force show mtv overlay
      {if {'||' {! $autoplay} {! $songtitle}} ;if not autoplay or if song title modifier is turned off
	     {script_task (delay 3) (units kTaskSeconds) ;wait 3 seconds
            (script {mtv_overlay_panel show_overlay FALSE})} ;and hide the mtv overlay
	  }
	  {if_else $towel ;check if towel hyperspeed modifier is enabled
	     {beatmatch set_music_speed 1.0} ;if enabled force the music speed to be 100%
	     {if_else {> $speedmod 1} ;if towel is disabled, check if song speed is greater than 100%
		    {beatmatch set_music_speed $speedmod} ;if music speed is set to greater than 100%, set the beatmatcher speed to that value
		    {beatmatch set_music_speed 1.0} ;if music speed is set to 100%, set beatmatcher speed to 100%
		 }
      }
	  {$this slide_meter_in} ;animate all of the hud meters in to position
      {if $do_streak {streak_overlay_panel setup_streak}} ;if streak is supposed to run, setup the streak meter
	  {streak_overlay_panel show_overlay $do_streak} ;show the streak meter if it is allowed to run
      kDataUnhandled)
   (extend_track ;ran after the intro camera shot, handles similar items to intro_skip, but this is ran when intro_skip is disabled
      ;{set $encorefocus FALSE} ;encores are not available in dx 3.0
      {world_panel set_showing {! $focusmode}} ;show the world panel or not if focus mode
      {if $focusmode {world do_force_shot lighter}} ;if focus mode, force a specific camera shot available in all venues
      {if {&& $focusmode {|| {== {gamecfg get mode} career} {== {gamecfg get mode} quickplay} {== {gamecfg get mode} multi_coop}}} ;if focus mode and not practice mode
         {if {exists world} {world toggle_pick_shot}} ;check if the world exists, and prevent the camera from leaving the lighter shot
      }
      {if {&& {! $neckless} {! $cheat_enabled_performance_mode}} ;if not neckless mode or performance mode
         {track_panel do_extend_sequence} ;extend the track out
	  }
      {if_else $towel ;check if towel hyperspeed modifier is enabled
	     {beatmatch set_music_speed 1.0} ;if enabled force the music speed to be 100%
	     {if_else {> $speedmod 1} ;if towel is disabled, check if song speed is greater than 100%
		    {beatmatch set_music_speed $speedmod} ;if music speed is set to greater than 100%, set the beatmatcher speed to that value
		    {beatmatch set_music_speed 1.0} ;if music speed is set to 100%, set beatmatcher speed to 100%
		 }
      }
      {script_task (delay 1.8) (units kTaskSeconds) ;wait 1.8 seconds
         (script {handle ($this slide_meter_in)}) ;animate the hud meters in
      }
      {script_task (delay 2) (units kTaskSeconds) ;wait 2 seconds
         (script {handle ($this intro_end) (streak_overlay_panel show_overlay $do_streak)}) ;run intro_end code and show the streak meter if it is running
      }
      {set [intro_complete] TRUE} ;this will allow the pause>restart button to restart back to the track extend, instead of the full intro shot every restart
   )
   (music_start {ui music_start} kDataUnhandled) ;start the music
   (beat ;runs code every beat of the currently playing song
      {if $barf_cheat ;check for roxbury modifier
         {track_panel animate (range 2000 2025) (period 0.4)} ;if roxbury enabled, shake the track on beat of the song
      }
      {if $debug.beep {play_sfx click.cue}} ;debug click track
      {if {! {game is_tutorial_running}} ;check if the tutorial is running
         {do
            ($clap FALSE) ;setup clap variable
            {$this foreach_player $p ;run the following code for each player currently enabled
               {if {$p in_star_mode} ;check if the current player has star power activated
                  {if_else $focusmode ;check if focus mode is enabled
                        {set $clap FALSE} ;if focus mode is enabled, do not clap on beat in star power
                        {set $clap TRUE} ;if focus mode is disabled, clap to the beat during star power
                  }
               }
            }
            {if $clap ;if clap turns to true after the above is ran
               {script_task (units kTaskBeats) (delay CLAP_DELAY_BEATS) ;run clap on beat
                  (script {handle (world play_sfx CLAP_SEQUENCE)}) ;play the audio for the clap
               }
			}
		  }
	   }
      kDataUnhandled
   )
   (downbeat ;runs code every down beat of the currently playing song
      {$this foreach_player $p {$p on_downbeat}} ;run the downbeat code for each player currently enabled
      kDataUnhandled
   )
   (game_over ;ran when song failed
      {$this foreach_player $p {$p on_game_over}}
      {stop_sfx sp_deployed} ;stop playing sp noisess
      {stop_sfx sp_deployed_multi} ;stop playing sp noisess
      {stop_sfx sp_deploystart.cue} ;stop playing sp noisess
      {streak_overlay_panel show_overlay FALSE} ;hide the streak overlay
      kDataUnhandled
   )
   (game_won_msg ;ran on song completion
      ;($encore) ;encores are not available in dx 3.0
      {set $author FALSE} ;we are done with the author flag for this song, reset to false
      {mtv_overlay_panel show_overlay FALSE} ;hide the mtv overlay now that the song is done
      {if_else $skipoutro
      0 ;do nothing if outro skipping is enabled
      {do
      {if {&& $focusmode {'||' {== {gamecfg get mode} career} {== {gamecfg get mode} quickplay} {== {gamecfg get mode} multi_coop}}} ;if focus mode and game mode is career or quickplay or coop
         {if {exists world} {world toggle_pick_shot}} ;check if the world exists, and allow camera cuts again
      }
      {world_panel set_showing TRUE} ;always show the world when a song is complete
	  {if_else {== {gamecfg get mode} practice} ;check if the current game mode is practice
		 0 ;if practice mode, do nothing
         {if_else {gamecfg get game_over_sequence} ;get the current game mode game_over_sequence
            {do
               #ifdef HX_XBOX ;xbox acheivement handling
               {if {! {gamecfg multiplayer}} ;if singleplayer
                  {achievements submit_points {player0 score}} ;submit singleplayer score to leaderboards
                  {if {== {player0 percent_hit} 100}
                     {achievements submit perfect}
                  }
                  {if {options get_lefty 0} ;if lefty flip enabled for player 1
                     {achievements submit lefty} ;pop up achievement
                  }
                  {if {&& {== {gamecfg get_difficulty 0} kDifficultyExpert} {== {gamecfg get_song} jordan}} ;if difficulty expert and the song shortname is "jordan"
                     {achievements submit jordan} ;pop up achievement
                  }
	           }
               {if {== multi_coop {gamecfg get mode}} ;check if the current gamemode is coop
                  {if {== {band percent_hit} 100} ;if all players hit 100% of the notes
                     {achievements submit_coop perfect} ;pop up achievement
                  }
                  {achievements submit_coop_points {band score}} ;submit coop score to leaderboards
               }
               #endif ;done with xbox specific code
               {track_panel explode} ;animate the track exploding
               {play_game_sfx TRACK_EXPLODE_SFX} ;track explode sound effect
               {game set_realtime TRUE} ;return to default game speed
               {beatmatch set_paused TRUE} ;pause the beatmatcher since it's exploded
               ;{if $encore {encore_stream play}} ;encores are not available in dx 3.0
               kDataUnhandled
	        }
        0} ;do nothing if there is no game_over_sequence defined
	  }}}
   )
   (game_lost ;run when the song is failed
      {set $author FALSE} ;set the author to false to be set again if restarted
      {if {&& $focusmode {'||' {== {gamecfg get mode} career} {== {gamecfg get mode} quickplay} {== {gamecfg get mode} multi_coop}}} ;if focus mode and game mode is career or quickplay or coop
         {if {exists world} {world toggle_pick_shot}}} ;check if the world exists, and allow camera cuts again
      {world_panel set_showing TRUE} ;set the world showing to true no matter what
      {track_panel set_showing FALSE} ;hide the track panel because you failed and you suck
      {game set_realtime TRUE} ;return to default game speed
      {$this slow_music} ;slow down the music based on LOSE_MUSIC_SLOW_SEC define
      #ifdef HX_XBOX ;xbox acheivement handling
      {if {&& {== {gamecfg get_difficulty 0} kDifficultyEasy} {! {gamecfg multiplayer}}} ;if the current difficulty is easy, and the mode is singleplayer
         {achievements submit faileasy} ;pop up achievement
      }
      #endif ;done with xbox specific code
      kDataUnhandled
   )
   (game_outro_complete ;run when the song outro is completed via fail or song complete
      {synth stop_all_sfx} ;stop all sound effects
      {game set_paused TRUE} ;freeze the game screen
      {if_else [won] ;check if the song was beaten or if you failed
         {do ;if beaten
            {ui goto_screen {if_else $auto_restart loading_screen post_show_screen}} ;either auto restart or go to post show screen that leads to endgame
         }
		 {do ;if failed
            {world_panel set_paused TRUE} ;freeze the world
		    {ui goto_screen {gamecfg get lose_screen}} ;go to the current mode designated lose screen noted in modes.dta
		 }
      }
      kDataUnhandled
   )
   (blew_big_note ;ran when you miss a specific "big note" defined in the midi
      ($player) ;setup player variable
      {if {crowd_audio enabled} {world play_sfx blew_bignote.cue} ;if crowd audio is enabled, play the sound effect
      }
   )
   #define PRACTICE_END_FADE_SEC (1) ;how many seconds until the practice end screen shows on section completion
   (practice_end ;ran at the end of selected sections in practice mode
      {music_fader fade -50 {'*' PRACTICE_END_FADE_SEC 1000}} ;fade out the audio when section is completed
      {script_task (delay PRACTICE_END_FADE_SEC) (units kTaskSeconds) ;wait until the music is faded out
         (script {$this set_paused TRUE} {ui goto_screen practice_end_screen}) ;freeze the game and pop up the practice end screen
      }
   )
   #define LOSE_MUSIC_SLOW_SEC (2) ;how many seconds the music slows down when you fail
   (slow_music ;ran when the player fails a song
      {set [lose_time] {taskmgr seconds}} ;start a timer for the slowdown
      {script_task (once FALSE) ;run this script repeatedly until the timer runs out
         (script
		    {do
               ($t {- {taskmgr seconds} [lose_time]}) ;setup time variable
               {if_else {< $t LOSE_MUSIC_SLOW_SEC} ;check if the timer has reached the defined period
                  {do
                     {beatmatch set_music_speed {/ {- LOSE_MUSIC_SLOW_SEC $t} LOSE_MUSIC_SLOW_SEC}} TRUE} ;slow down the music based off the current timer
                  {do
                     {beatmatch set_paused TRUE} FALSE} ;if the timer has ended, pause the music completely
			   }
			}
		  )
      }
	)
   (end_streak ;ran when a note streak has been broken
      ($player $playerobj) ;setup variables
      {if {gamecfg get blown_streak_feedback} ;if the current mode allows blown streak feedback
         {do
            ($mult) ;setup variable
            {if_else {== multi_coop {gamecfg get mode}} ;check if the current game mode is coop
               {set $mult {band base_multiplier}} ;if coop, check the whole band multiplier
               {set $mult {$playerobj base_multiplier}} ;if singleplayer, check only the first player
			}
            {if {'&&' $highwayshake {> $mult 1}} ;if highwayshake is enabled and the multiplier is above 1x
               {handle (game blow_streak $player $mult)} ;shake the highway for the player in severity based off the current multiplier
			}
         }
       }
   )
   (blow_streak ;ran when a player misses in a streak
      ($player $mult) ;setup the variables
      {do
         ($sfx {sprintf "streak_broken_%ix.cue" $mult}) ;setup which sound to use via variables
         {if $sfx {play_game_sfx_player $sfx $sfx $player}} ;only play miss streak sound effects if focus mode is not enabled
		 }
      kDataUnhandled
   )
   (force_encore FALSE) ;encores are not available in dx 3.0
   (lose_time 0) ;sets lose timer default
   (intro_complete FALSE) ;game defaults intro complete to false and script sets up later
}
{new UIPanel world_panel
   (file
      {sprint "../world/" {game get_venue} "/" {game get_venue} ;handle choosing the currently selected venue and parsing it's location on ark
         {if_else {game multiplayer} ;check if the game is coop
            {if_else {game multiplayer_vs} ;check if the game is faceoff
               "_mp.milo" ;load faceoff venue milos
               "_coop.milo"} ;load coop venue milos
            ".milo"} ;load standard venue milos
      }
   )
   (finish_load
      {{$this loaded_dir} set_name world main} ;set the world name to "main"
      #ifdef HX_XBOX {world set echo_msgs $echo_world_events} #endif ;handle world events if on xbox
   )
   (enter ;when the screen is entered
      {if $focusmode {crowd_audio set_enabled FALSE}} ;if focus mode is enabled, mute the crowd audio
      {if_else {game multiplayer} ;check if the game is coop/pro/faceoff
         {handle ($this multi_player)} ;multi player code handle
         {handle ($this single_player)} ;single player code handle
      }
      {$this set_showing {! $focusmode}} ;hide the world if focus mode is disabled?
      {$this iterate PostProc $p {$p set emulate_fps $emulatefps}} ;set the venue fps to the defined fps
      {$this set_paused FALSE} ;unfreeze the world screen
   )
      #ifdef HX_XBOX (unload {rnd suspend}) #endif ;no idea but ps2 does not have this code
}
#define FADE_LENGTH_SEC (2)
{new FadePanel fade_panel
   (file fade.milo)
   (enter ;when the screen is entered
      {do
         {press_any_button.lbl set_showing FALSE} ;attract is not available in dx 3.0 so always hide the label and mesh
         #ifdef HX_XBOX {logo.mesh set_showing FALSE} #endif} ;attract is not available in dx 3.0 so always hide the label and mesh
   )
}
{new GHScreen finish_fade
   (panels fade_panel) ;panels used in this screen, in draw order
   (enter ;when the screen is entered
      {meta_loading_goto main_screen} ;go to main screen, previously splash screen
   )
}
#define GAME_PANELS (midi_loader_panel game track_mask world_panel track_panel hud mtv_overlay_panel streak_overlay_panel) ;define the panels used in the game screen, in draw order
{new GHScreen game_screen
   (panels GAME_PANELS fade_panel) ;panels used in this screen, in draw order
   (allow_back FALSE) ;set no default back screen when this screen is loaded
   (animate_transition FALSE) ;do not animate to this screen, cut to it
   (in_game TRUE) ;defines this screen as one that runs during game logic
   (attract_mode FALSE) ;attract mode is not used in dx 3.0
   (BUTTON_DOWN_MSG ;code to run when a specific button is pressed
      {if {== $button kPad_Start} ;if start is pressed
         {game set_paused TRUE} ;freeze the game
		 {ui goto_screen pause_screen} ;open the pause screen
      }
   )
   (TRANSITION_COMPLETE_MSG ;run when the pause screen is closed
      {if $game_unpause_on_enter ;check if we are entering the game from the pause screen
         {game set_paused FALSE} ;unfreeze the game
         {world_panel set_paused FALSE} ;unfreeze the world
         {set $game_unpause_on_enter FALSE} ;reset the unpause location state
      }
   )
   #ifdef HX_XBOX (enter {track_mask set_showing TRUE}) #endif ;show the track mask, only runs on xbox
}
{new GHScreen reload_track_screen
   (panels game world_panel hud mtv_overlay_panel fade_panel) ;panels used in this screen, in draw order
   (allow_back FALSE) ;set no default back screen when this screen is loaded
   (animate_transition FALSE) ;do not animate to this screen, cut to it
   (enter ;when the screen is entered
      {game set_paused TRUE} ;freeze the game
      {game_restart_fast} ;quick restart the current song
   )
}