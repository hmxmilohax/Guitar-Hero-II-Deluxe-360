#include game_track_panel.dta
#include game_hud_panel.dta
#define DRUMMER_EXPLODE_DELAY (3.0)

{new GHPanel mtv_overlay_panel
   (file mtv_overlay.milo)
   (enter
      {if_else {game is_tutorial_running}
         {$this show_overlay FALSE}
         {$this setup_text}})
   (setup_text
      {do
         ($song {gamecfg get_song})
         ($song_text {game get_song_text})
         ($artist_text {game get_song_artist_text})
         ($song_caption {game get_song_caption})
         {do
            ($prefix "mtv_campaign_line")
            {mtv_campaign_song_id.view set_showing TRUE}
            {if_else {!= $speedmod 1}
               {if_else {&& {==
               {gamecfg get mode} practice}
               {!= {gamecfg get practice_speed} 0}}
                  {$this set_line $prefix 1 $song_text}
                  {$this set_line $prefix 1 {sprint $song_text " (" {int {'+' 0.5 {'*' $speedmod 100}}} "%)"}}}
               {$this set_line $prefix 1 $song_text}}
            {$this set_line $prefix 2 $song_caption}
#ifndef 2DBUG
#ifndef 8DBUG
            {if_else {!= "" {localize {sprint {gamecfg get_song} "_author"}}} ; If not debug, check for author string
               {$this set_line $prefix 3 {sprint $artist_text "\nAuthor: " {localize {sprint $song "_author"}}}} ; If present, add newline to line 3 followed by author
               {if_else $author ; check for a set $author in DLC contexts.dta
               {$this set_line $prefix 3 {sprint $artist_text "\nAuthor: " $author}} ;                             ^
               {$this set_line $prefix 3 {sprint $artist_text "\nAuthor: Unknown"}}}} ; If not, put unknown
#endif
#endif
; If debug, just put artist text
#ifdef 2DBUG {$this set_line $prefix 3 $artist_text} #endif
#ifdef 8DBUG {$this set_line $prefix 3 $artist_text} #endif
   }})
   (set_line
      ($prefix $i $text)
      {{sprint $prefix $i ".lbl"} set_localized_text $text}
      {{sprint $prefix $i "_shadow.lbl"} set_localized_text $text})
   (show_overlay
      ($show)
      {$this set_showing $show}
      {$this set_paused {! $show}})}
{new MidiLoaderPanel midi_loader_panel}
#define LOSE_MUSIC_SLOW_SEC (2)
#define PRACTICE_END_FADE_SEC (1)
{new GamePanel game
   (file "")
   (track_extend_sec -2)
   (encore_unlock_check FALSE)
   (encore_song FALSE)
   (load
      {set_loader_period 13})
   (enter
      #ifdef HX_XBOX
      {rnd suspend}
      #endif
      {set $interval_p0 50}
      {set $interval_p1 50}
      {set $interval_p2 50}
      {set $player0_streak 0}
      {set $player1_streak 0}
      {set $player2_streak 0}
      {set_loader_period 5}
      {set [encore_unlock_check]
         {&& {==
            {gamecfg get mode} career}
            {campaign encore_unlock_potential}}}
      {print "encore unlock check is " [encore_unlock_check] "\n"}
      {set [encore_song]
         {&&
            {!= {gamecfg get mode} tutorial}
            {campaign is_encore_song {gamecfg get_song}}}}
      {print "encore song is " [encore_song] "\n"})
   (intro_start_msg
      ($fast $encore)
      {track_panel intro_start_msg}
      {mtv_overlay_panel show_overlay FALSE}
      {streak_overlay_panel show_overlay FALSE}
      {switch
         {gamecfg get mode}
         (multi_vs {set $songtitle FALSE})
         (multi_fo {set $songtitle FALSE})}
      {if $encore
         {do
            {world_panel set_showing TRUE}
            {if $focusmode
               {set $encorefocus TRUE}}}}
      {if_else {! $fast}
         {do
            {if $do_streak {streak_overlay_panel setup_streak}}
            {script_task
               (delay 1)
               (units kTaskSeconds)
               (script {mtv_overlay_panel show_overlay TRUE})}
            {script_task
               (delay 6)
               (units kTaskSeconds)
               (script {mtv_overlay_panel show_overlay $songtitle})}}
         {do
            {if $do_streak {streak_overlay_panel setup_streak}}
            {script_task
               (delay 1)
               (units kTaskSeconds)
               (script {mtv_overlay_panel show_overlay TRUE})}
            {script_task
               (delay 3)
               (units kTaskSeconds)
               (script {mtv_overlay_panel show_overlay $songtitle})}}}
      {set [intro_complete] FALSE}
      kDataUnhandled)
   (intro_skip
      {track_panel intro_skip}
	  {beatmatch set_music_speed $speedmod}
      {if $do_streak {streak_overlay_panel setup_streak}}
      kDataUnhandled)
   (extend_track
      {set $encorefocus FALSE}
      {world_panel set_showing {! $focusmode}}
      {if $focusmode
         {world do_force_shot lighter}}
      {if {&& $focusmode {||
      {== {gamecfg get mode} career}
      {== {gamecfg get mode} quickplay}
      {== {gamecfg get mode} multi_coop}}}
         {if {exists world}
            {world toggle_pick_shot}}}
      {if {&&
      {! $neckless}
      {! $cheat_enabled_performance_mode}}
         {track_panel do_extend_sequence}}
      {beatmatch set_music_speed $speedmod}
      {script_task
         (delay 1.8)
         (units kTaskSeconds)
         (script {handle ($this slide_meter_in)})}
      {script_task
         (delay 2)
         (units kTaskSeconds)
         (script {handle
               ($this intro_end)
               (streak_overlay_panel show_overlay $do_streak)})}
      {set [intro_complete] TRUE})
   (music_start {ui music_start} kDataUnhandled)
   (beat
      {if $barf_cheat
         {track_panel animate
            (range 2000 2025)
            (period 0.4)}}
      {if $debug.beep
         {play_sfx click.cue}}
      {if {! {game is_tutorial_running}}
         {do
            ($clap FALSE)
            {$this foreach_player $p
               {if {$p in_star_mode}
                  {if_else $focusmode
                        {set $clap FALSE}
                        {set $clap TRUE}}}}
            {if $clap
               {script_task
                  (units kTaskBeats)
                  (delay CLAP_DELAY_BEATS)
                  (script {handle (world play_sfx CLAP_SEQUENCE)})}}}}
      kDataUnhandled)
   (downbeat
      {$this foreach_player $p {$p on_downbeat}}
      kDataUnhandled)
   (game_over
      {$this foreach_player $p {$p on_game_over}}
      {stop_sfx sp_deployed}
      {stop_sfx sp_deployed_multi}
      {stop_sfx sp_deploystart.cue}
      {streak_overlay_panel show_overlay FALSE}
      kDataUnhandled)
   (game_won_msg
      ($encore)
      {set $author FALSE}
      {mtv_overlay_panel show_overlay FALSE}
      {if {&&
      $focusmode
      {||
         {== {gamecfg get mode} career}
         {== {gamecfg get mode} quickplay}
         {== {gamecfg get mode} multi_coop}}}
         {if {exists world}
            {world toggle_pick_shot}}}
      {world_panel set_showing TRUE}
	  {if_else
         {== {gamecfg get mode} practice}
		 0
      {if_else
         {gamecfg get game_over_sequence}
         {do
            {if {== {gamecfg get_song} tonightimgonna}
               {do
                  {script_task
                     (delay DRUMMER_EXPLODE_DELAY)
                     (units kTaskSeconds)
                     (script
                        {handle ($this drummer_explode)}
                        {play_sfx drummer_exp}
                        {handle ({{world_panel loaded_dir} find guitarist0} game_won_msg $encore)})}}}
#ifdef HX_XBOX
{if
               {!
                  {gamecfg multiplayer}}
               {achievements
                  submit_points
                  {player0 score}}
               {if
                  {==
                     {player0 percent_hit}
                     100}
                  {achievements submit perfect}}
               {if
                  {options get_lefty 0}
                  {achievements submit lefty}}
               {if
                  {&&
                     {==
                        {gamecfg get_difficulty 0}
                        kDifficultyExpert}
                     {==
                        {gamecfg get_song}
                        jordan}}
                  {achievements submit jordan}}}
            {if
               {==
                  multi_coop
                  {gamecfg get mode}}
               {if
                  {==
                     {band percent_hit}
                     100}
                  {achievements submit_coop perfect}}
               {achievements
                  submit_coop_points
                  {band score}}}
#endif
            {track_panel explode}
            {play_game_sfx TRACK_EXPLODE_SFX}
            {game set_realtime TRUE}
            {beatmatch set_paused TRUE}
            {if
               $encore
               {encore_stream play}}
            kDataUnhandled}
		 0}})
   (game_lost
      {set $author FALSE}
      {if {&&
      $focusmode
      {||
         {== {gamecfg get mode} career}
         {== {gamecfg get mode} quickplay}
         {== {gamecfg get mode} multi_coop}}}
         {if {exists world}
            {world toggle_pick_shot}}}
      {world_panel set_showing TRUE}
      {track_panel set_showing FALSE}
      {game set_realtime TRUE}
      {$this slow_music}
#ifdef HX_XBOX
      {if
         {&&
            {==
               {gamecfg get_difficulty 0}
               kDifficultyEasy}
            {!
               {gamecfg multiplayer}}}
         {achievements submit faileasy}}
#endif
      kDataUnhandled)
   (game_outro_complete
      {synth stop_all_sfx}
      {game set_paused TRUE}
      {if_else
         [won]
         {do
            {if {&&
            {== {gamecfg get mode} career}
            {campaign final_song}}
               {meta set won_campaign TRUE}}
            {ui goto_screen
               {if_else $auto_restart
                  loading_screen
                  post_show_screen}}}
         #ifndef HX_XBOX
		 {ui
            goto_screen
            {gamecfg get lose_screen}}
		 #else
		 {do
            {world_panel set_paused TRUE}
	     
		 {ui
            goto_screen
            {gamecfg get lose_screen}}}
		#endif}
      kDataUnhandled)
   (blew_big_note
      ($player)
      {if {crowd_audio enabled}
         {world play_sfx blew_bignote.cue}})
   (practice_end
      {music_fader fade -50 {'*' PRACTICE_END_FADE_SEC 1000}}
      {script_task
         (delay PRACTICE_END_FADE_SEC)
         (units kTaskSeconds)
         (script
            {$this set_paused TRUE}
               {ui goto_screen practice_end_screen})})
   (slow_music
      {set [lose_time] {taskmgr seconds}}
      {script_task
         (once FALSE)
         (script
            {do
               ($t {- {taskmgr seconds} [lose_time]})
               {if_else {< $t LOSE_MUSIC_SLOW_SEC}
                  {do
                     {beatmatch set_music_speed {/ {- LOSE_MUSIC_SLOW_SEC $t} LOSE_MUSIC_SLOW_SEC}}
                     TRUE}
                  {do
                     {beatmatch set_paused TRUE}
                     FALSE}}})})
   (end_streak
      ($player $playerobj)
      {if {gamecfg get blown_streak_feedback}
         {do
            ($mult)
            {if_else
               {== multi_coop {gamecfg get mode}}
               {set $mult {band base_multiplier}}
               {set $mult {$playerobj base_multiplier}}}
            {if $highwayshake {if {> $mult 1}
            {handle (game blow_streak $player $mult)}}}
			   }})
   (blow_streak
      ($player $mult)
      {do
         ($sfx
            {sprintf "streak_broken_%ix.cue" $mult})
         {if {! $focusmode} {play_game_sfx_player $sfx $sfx $player}}
		 }
      kDataUnhandled)
   (cheat_force_encore
      {cheat_display show "Forcing encore intro/outro"}
      {set [force_encore] TRUE})
   (force_encore FALSE)
   (lose_time 0)
   (intro_complete FALSE)}
{new UIPanel world_panel
   (file
      {sprint "../world/" {game get_venue} "/" {game get_venue}
         {if_else {game multiplayer}
            {if_else {game multiplayer_vs}
               "_mp.milo"
               "_coop.milo"}
            ".milo"}})
   (finish_load
      {{$this loaded_dir} set_name world main}
      #ifdef HX_XBOX {world set echo_msgs $echo_world_events} #endif)
   (enter
      {if $focusmode {crowd_audio set_enabled FALSE}}
      {if_else {game multiplayer}
         {handle ($this multi_player)}
         {handle ($this single_player)}}
      {$this set_showing {! $focusmode}}
      {$this iterate PostProc $p {$p set emulate_fps $emulatefps}}
      {$this set_paused FALSE})
      #ifdef HX_XBOX
   (unload {rnd suspend})
   #endif}
#define ATTRACT_LENGTH_SEC (120)
#define FADE_LENGTH_SEC (2)
{new FadePanel fade_panel
   (file fade.milo)
   (enter
      {do
         ($attract
            {==
               {gamecfg get mode}
               attract})
         {press_any_button.lbl set_showing $attract}
         #ifdef HX_XBOX {logo.mesh set_showing $attract} #endif})}
#ifndef HX_XBOX
#define TRACK_MASK ()
#endif
#define GAME_PANELS (midi_loader_panel game #ifndef HX_XBOX TRACK_MASK #else track_mask #endif world_panel track_panel hud mtv_overlay_panel streak_overlay_panel)
{new GHScreen game_screen
   (panels GAME_PANELS fade_panel)
   (allow_back FALSE)
   (animate_transition FALSE)
   (in_game TRUE)
   (attract_mode FALSE)
   (BUTTON_DOWN_MSG
      {if_else
         [attract_mode]
         {if
            {meta is_any_button $button $player_num}
            {set
               $leaving
               {-
                  {taskmgr ui_seconds}
                  $enter_time}}}
         {if
            {&&
               {== $button kPad_Start}
               {!
                  {game get game_over}}}
            {$this pause_game}}})
   (pause_game
      {game set_paused TRUE}
      {ui goto_screen pause_screen})
   (TRANSITION_COMPLETE_MSG
      {if $game_unpause_on_enter
         {game set_paused FALSE}
         {world_panel set_paused FALSE}
         {set $game_unpause_on_enter FALSE}})
   (enter #ifdef HX_XBOX {track_mask set_showing TRUE} #endif)}
{new GHScreen finish_fade
   (panels fade_panel)
   (enter
      {meta_loading_goto splash_screen})}
{new GHScreen reload_track_screen
   (panels game world_panel hud mtv_overlay_panel fade_panel)
   (allow_back FALSE)
   (animate_transition FALSE)
   (enter
      {game set_paused TRUE}
      {cheat_display show "Reloading track..."}
      {game_restart_fast})}
{new
   GHScreen
   glitch_screen
   (panels)
   (enter
      {print
         "entered glitch screen"})}